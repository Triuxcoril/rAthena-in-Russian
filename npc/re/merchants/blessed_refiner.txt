//===== rAthena Script ======================================= 
//= Blessed Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiners that use Blessed ores to refine equipment.
//= Upon failure, the equipment is not destroyed. The success
//= rate is identical to that for Enriched ores.
//= - "Кузнец Дистер" only refines from +6~12.
//= NOTE: This NPC is currently disabled on official servers.
//===== Changelog: ===========================================
//= 1.0 First version. [Euphy]
//= 1.1 Removed re-roll behavior. [Secret]
//============================================================
//= 22.05.2021 Перевод на русский [Xsoid]
//============================================================

// Main NPC :: new_smelting612
//============================================================
-	script	::BlacksmithDister	-1,{
	disable_items;
	mes "[Кузнец Дистер]";
	mes "В этом высококонкурентном обществе мы должны быть лучше, чтобы выжить!";
	mes "Я улучшаю экипировку только на уровнях +6 ~ 12.";
	next;
	mes "[Кузнец Дистер]";
	mes "Мне понадобится ^ff9999Благословенный Оридекон [Blessed Oridecon]^000000 для оружия и ^ff9999Благословенный Элуний [Blessed Elunium]^000000 для доспехов.";
	mes "Неудачная заточка ^FF0000не приведет к снижению уровня заточки^000000.";
	next;
	mes "[Кузнец Дистер]";
	mes "Ну что? Что будем улучшать?";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[Unequipped]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Кузнец Дистер]";
		switch(.@part) {
		case EQI_HEAD_TOP:
			mes "Я кузнец, а не парикмахер.";
			break;
		case EQI_ARMOR:
			mes "Своим молотом я сделаю тебя небесной звездой.";
			break;
		case EQI_HAND_L:
		case EQI_HAND_R:
			mes "Точить руки не моя задача, а твоя.";
			break;
		case EQI_GARMENT:
			mes "Принеси предмет, чтобы я мог его усовершенствовать!";
			break;
		case EQI_SHOES:
			mes "Откуда этот странный запах?";
			break;
		case EQI_ACC_L:
		case EQI_ACC_R:
			mes "Где аксессуар?";
			break;
		case EQI_HEAD_MID:
			mes "Что ты хочешь, чтобы я улучшил?";
			break;
		case EQI_HEAD_LOW:
			mes "А? Что ты хочешь, чтобы я сделал?";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[Кузнец Дистер]";
		mes "Этот предмет не может быть улучшен.";
		close;
	}
	.@equip_id = getequipid(.@part);
	.@itemtype = getiteminfo( .@equip_id, ITEMINFO_TYPE );
	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	if (.@equip_refine < 6 || .@equip_refine > 12) {
		mes "[Кузнец Дистер]";
		mes "Это снаряжение было улучшено до "+.@equip_refine+". Я работаю только с экипировкой уровнем заточки от +6 до +12!";
		close;
	}
	if( .@itemtype == IT_ARMOR ){
		.@equip_lv = getequiparmorlv( .@part );
		.@type$ = "Броня";

		switch(.@equip_lv) {
			case 1:
				set .@price,20000;
				set .@material,6439; //Unbreakable_Def
				break;
			default:
				// TODO:
				close;
		}
	}else if( .@itemtype == IT_WEAPON ){
		.@equip_lv = getequipweaponlv( .@part );
		.@type$ = "Оружие";

		switch( .@equip_lv ){
			case 1:
				set .@price,1000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 2:
				set .@price,2000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 3:
				set .@price,20000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 4:
				set .@price,40000;
				set .@material,6438; //Unbreakable_Weap
				break;
			default:
				// TODO:
				close;
		}
	}else{
		// TODO:
		close;
	}
	set .@ore$,"^ff9999Blessed "+.@type$+" Ore^000000";
	mes "[Кузнец Дистер]";
	mes "Это "+.@type$+" было заточено до "+.@equip_refine+". Что бы заточить его, мне нужно "+.@ore$+" и "+callfunc("F_InsertComma",.@price)+" зен.";
	mes "Готов продолжить?";
	next;
	if(select("Да:Нет") == 2) {
		mes "[Кузнец Дистер]";
		mes "Тогда не трать моё время!";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[Кузнец Дистер]";
		mes "Это "+.@type$+" было улучшено много раз. При не удаче он не сломается, но "+.@ore$+" будет потрачен!";
		next;
		mes "[Кузнец Дистер]";
		mes "Конечно, оплату я то же не верну! Продолжаем?";
		next;
		if(select("Да:Нет") == 2) {
			mes "[Кузнец Дистер]";
			mes "Хорошо. Я бы расстроился, если бы не справился.";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[Кузнец Дистер]";
		mes "Материалов недостаточно.";
		mes "Для заточки "+.@type$+" "+.@equip_lv+" уровня, мне нужно "+.@ore$+" и "+callfunc("F_InsertComma",.@price)+" зен.";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	mes "[Кузнец Дистер]";
	mes "Так! Так! Так!";
	if (getequippercentrefinery(.@part, true) > rand(100)) {
		specialeffect EF_BLESSING;
		successrefitem .@part;
		next;
		mes "[Кузнец Дистер]";
		mes "Хахахахахаха!!!";
		next;
		mes "[Кузнец Дистер]";
		mes "Заточка имела огромный успех~ Хахаха!";
		close;
	}
	specialeffect EF_CURSEATTACK;
	specialeffect2 EF_SUI_EXPLOSION;
	next;
	emotion (!rand(5))?ET_ANGER:ET_HUK;
	mes "[Кузнец Дистер]";
	mes "О мой бох!!!!";
	next;
	mes "[Кузнец Дистер]";
	mes "О! Хорошо что мы используем "+.@ore$+". Это позволило спасти снаяржение...";
	next;
	mes "[Кузнец Дистер]";
	mes "Я в печали, когда меня настигает неудача! Уходи!";
	close;
}
prt_in,52,56,3	duplicate(BlacksmithDister)	Кузнец Дистер#prt	826
payon,148,172,3	duplicate(BlacksmithDister)	Кузнец Дистер#pay	826
alberta_in,20,56,3	duplicate(BlacksmithDister)	Кузнец Дистер#alb	826
yuno_in01,175,18,3	duplicate(BlacksmithDister)	Кузнец Дистер#yuno	826
ein_in01,26,82,3	duplicate(BlacksmithDister)	Кузнец Дистер#ein	826
lhz_in02,280,15,3	duplicate(BlacksmithDister)	Кузнец Дистер#lhz	826
