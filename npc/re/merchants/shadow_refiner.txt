//===== rAthena Script =======================================
//= Теневой Кузнец
//===== Description: =========================================
//= [Official Conversion]
//= Shadow equipments refining NPC.
//===== Changelog: ===========================================
//= 1.0 First version [Aleos]
//= 1.1 Removed re-roll behavior and fetch materials from db
//=     [Secret]
//= 1.2 Added Refine UI [Atemo, Lemongrass]
//============================================================
//= 22.05.2021 Перевод на русский [Xsoid]
//============================================================

-	script	::ShadowBlacksmith	-1,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Теневой Кузнец]";
		mes "Теперь Баста отвечает за доработку теневых предметов.";
		mes "Если ты хочешь усовершенствовать теневые предметы, посети его.";
		close;
	}

	.@zeny_cost = 200000; // Zeny cost is 200,000 according to official script [Secret]
	disable_items;
	mes "[Теневой Кузнец]";
	mes "Ты хочешь улучшить теневой элемент? Выбери свой яд!";
	next;
	setarray .@indices[1], EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_R, EQI_SHADOW_ACC_L;
	.@indlen = getarraysize(.@indices) - 1;
	for(.@i = 1; .@i <= .@indlen; .@i++)
		.@menu$ = .@menu$ + (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[Not equipped]") +":";
	.@menu$ = .@menu$ + "Refine info";
	.@choice = select(.@menu$);
	if (.@choice == .@indlen + 1) { // Refine info
		mes "[Теневой Кузнец]";
		mes "Когда теневой предмет улучшается, он получает дополнительные бонусы, очень похожие на обычные предметы.";
		next;
		mes "[Теневой Кузнец]";
		mes "Оружие: АТК, МАТК +1 увеличение за каждое улучшение +1.";
		mes "Прочие: увеличение HP +10 за каждое улучшение +1.";
		next;
		mes "[Теневой Кузнец]";
		mes "Все типы Oridecon и Elunium могут быть использованы для улучшения теневых предметов. Каждая попытка будет стоить 20 000 зен.";
		next;
		mes "[Теневой Кузнец]";
		mes "HD-руды можно использовать для снаряжения, которое имеет по крайней мере 7-й уровень заточки и не будет ломаться, пока ты разговариваешь со мной.";
		close;
	}

	.@part = .@indices[.@choice];

	if (!getequipisequiped(.@part)) {
		mes "[Теневой Кузнец]";
		mes "Здесь ничего нет!";
		close;
	}
	.@equip_id = getequipid(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	while(1) {
		.@equip_refine = getequiprefinerycnt(.@part);
		mes "[Теневой Кузнец]";
		mes "Мне нужно " + callfunc("F_InsertComma", .@zeny_cost) + " зен за КАЖДУЮ попытку заточки.";
		mes "Выбери какую используем руду и преступим к делу.";
		next;

		.@material[0] = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
		.@material[1] = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);
		.@material[2] = getequiprefinecost(.@part, REFINE_COST_HD, REFINE_MATERIAL_ID);
		.@is_enriched = false;

		if (countitem(.@material[0]))
			.@mate$[0] = getitemname(.@material[0]);
		else
			.@mate$[0] = "^8C8C8C"+ getitemname(.@material[0]) +"^000000";
		if (countitem(.@material[1]))
			.@mate$[1] = getitemname(.@material[1]);
		else
			.@mate$[1] = "^8C8C8C"+ getitemname(.@material[1]) +"^000000";
		if (.@equip_refine > 6 && countitem(.@material[2]))
			.@mate$[2] = getitemname(.@material[2]);
		else
			.@mate$[2] = "^8C8C8C"+ getitemname(.@material[2]) +"^000000";

		switch( select( .@mate$[0], .@mate$[1], .@mate$[2], "Cancel" ) ) {
		case 1:// NORMAL
			.@choose = .@material[0];
			break;
		case 2:// ENRICHED
			.@is_enriched = true;
			.@choose = .@material[1];
			break;
		case 3:// HD
			if (.@equip_refine < 7) {
				mes "[Теневой Кузнец]";
				mes "HD-руда может быть использована только для предметов уровня очистки +7 или выше.";
				close;
			}
			.@hoihoi = 1;
			.@choose = .@material[2];
			break;
		case 4:
			mes "[Теневой Кузнец]";
			mes "Ты отменил заточку";
			close;
		}
		if (!countitem(.@choose)) {
			mes "[Теневой Кузнец]";
			mes "Тебе не хватает "+ getitemname(.@choose) +".";
			close;
		}
		if (Zeny < .@zeny_cost) {
			mes "[Теневой Кузнец]";
			mes "Тебе не хватает зени.";
			close;
		}
		if (.@equip_refine > 9) {
			mes "[Теневой Кузнец]";
			mes "Теневое снаяржение может быть улучшено максимум до 10...";
			close;
		}
		if (!getequipisenableref(.@part)) {
			mes "[Теневой Кузнец]";
			mes "Этот предмет не может быть улучшен.";
			close;
		}
		if (getequippercentrefinery(.@part) < 100) {
			mes "[Теневой Кузнец]";
			mes "Уровень безопастной заточки для Теневого снаряжения составляет +4.";
			if (!.@hoihoi)
				mes "Теневое снаряжение может быть уничтожено при следующем улучшении. Продолжить?";
			else
				mes "Следующее улучшение, при неудаче, приведет к снижению уровню заточки. Продолжаем?";
			next;
			if (select("Продолжаем","Отмена") == 2) {
				mes "[Теневой Кузнец]";
				mes "Хех, я так и знал!";
				close;
			}
		}
		mes "[Теневой Кузнец]";
		mes "Ну значит... тут ничего не происходит!";
		next;
		delitem .@choose,1;
		Zeny -= .@zeny_cost;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
			close;

		if (getequippercentrefinery(.@part, .@is_enriched) > rand(100)) {
			successrefitem .@part;
			mes "[Теневой Кузнец]";
			mes "Это сработало! Это сработало!";
			next;
		} else {
			if (.@hoihoi)
				downrefitem .@part;
			else
				failedrefitem .@part;
			mes "[Теневой Кузнец]";
			mes "О, Один, Нет!";
			close;
		}
	}
}

//moc_paraup,45,185,5	duplicate(ShadowBlacksmith)	Теневой Кузнец#eden1	4_F_JOB_BLACKSMITH // Commented out until it's added to the map index
prt_in,61,54,3	duplicate(ShadowBlacksmith)	Теневой Кузнец#itemmall	4_F_JOB_BLACKSMITH
