//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================
//= Перевод Xsoid
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
prontera,184,177,6	script	Мастер Совершенства	851,{
	disable_items;
	setarray .@cert_weapon[0],
		6864,	// Guarantee_Weapon_19Up
		6875,	// Guarantee_Weapon_18Up
		6874,	// Guarantee_Weapon_17Up
		6873,	// Guarantee_Weapon_16Up
		6872,	// Guarantee_Weapon_15Up
		6871,	// Guarantee_Weapon_14Up
		6870,	// Guarantee_Weapon_13Up
		6584,	// Guarantee_Weapon_12Up
		6238,	// Guarantee_Weapon_11Up
		6228,	// Guarantee_Weapon_9Up
		6229,	// Guarantee_Weapon_8Up
		6230,	// Guarantee_Weapon_7Up
		6231,	// Guarantee_Weapon_6Up
		6456;	// Guarantee_Weapon_5Up
	setarray .@cert_armor[0],
		6865,	// Guarantee_Armor_19Up
		6881,	// Guarantee_Armor_18Up
		6880,	// Guarantee_Armor_17Up
		6879,	// Guarantee_Armor_16Up
		6878,	// Guarantee_Armor_15Up
		6877,	// Guarantee_Armor_14Up
		6876,	// Guarantee_Armor_13Up
		6585,	// Guarantee_Armor_12Up
		6239,	// Guarantee_Armor_11Up
		6232,	// Guarantee_Armor_9Up
		6233,	// Guarantee_Armor_8Up
		6234,	// Guarantee_Armor_7Up
		6235,	// Guarantee_Armor_6Up
		6457;	// Guarantee_Armor_5Up
	setarray .@cert_level[0],19,18,17,16,15,14,13,12,11,9,8,7,6,5;
	.@size_cert = getarraysize(.@cert_weapon);

	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@cert_weapon[.@i]) > 0 || countitem(.@cert_armor[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}

	if (.@check == 0) {
		mes "[Мастер Совершенства]";
		mes "Здравствуйте!";
		mes "Что вам нужно?";
		mes "Я специалист по доработке предметов, но больше не работаю.";
		next;
		switch(select("Хорошо, я пошёл.:Хмм... точно?")) {
		case 1:
			mes "[Мастер Совершенства]";
			mes "Береги себя, авантюрист.";
			close;
		case 2:
			mes "[Мастер Совершенства]";
			mes "Ну, иногда я всё-таки оказываю услуги тем, у кого есть ^006400Сертификат на улучшение [Refine Ticket]^000000...";
			mes "Так что, давай, до свиданья~!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[Мастер Совершенства]";
	mes "Приветствую!";
	mes "Я могу улучшить предмет ^006400до уровня указанного на сертификате^000000.";
	mes "Волноваться не о чем! Ничего не сломается.";
	next;
	if(select("Я вернусь позже.:Уточнить предмет.") == 1) {
		mes "[Мастер Совершенства]";
		mes "Хорошо.";
		mes "Приходите позже.";
		close;
	}
	mes "[Мастер Совершенства]";
	mes "Какое оборудование вы хотели бы улучшить?";
	next;
	setarray .@position$[1],"Шапочка [Head upper]","Броня [Armor]","Левая рука [Left hand]","Правая рука [Right hand]","Накидка [Robe]","Обувь [Shoes]","Аксессуар-1 [Accessory 1]","Аксессуар-2 [Accessory 2]","Середина лица [Head middle]","Нижняя часть лица [Head lower]";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for ( .@i = 1; .@i <= 10; ++.@i )
		.@menu$ += (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : .@position$[.@i] + "- [Empty]") + ":";

	.@part = .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Мастер Совершенства]";
		mes "Вы должны экипировать предмет, который хотите улучшить.";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[Мастер Совершенства]";
		mes "Прошу прощения.";
		mes "Этот предмет невозможно улучшить.";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );
	if( .@itemtype == IT_WEAPON ){
		switch( getequipweaponlv( .@part ) ){
			case 1:
			case 2:
			case 3:
			case 4:
				copyarray .@tickets[0], .@cert_weapon[0], .@size_cert;
				.@type$ = "Weapon";
				break;
			default:
				// TODO:
				close;
		}
	}else if( .@itemtype == IT_ARMOR ){
		switch( getequiparmorlv( .@part ) ){
			case 1:
				copyarray .@tickets[0], .@cert_armor[0], .@size_cert;
				.@type$ = "Armor";
				break;
			default:
				// TODO:
				close;
		}
	}else{
		// TODO:
		close;
	}
	.@check = 0;
	for ( .@i = 0; .@i < .@size_cert; ++.@i ) {
		if (countitem(.@tickets[.@i]) > 0) {
			.@check = 1;
			break;
		}
	}
	if (.@check == 0) {
		emotion ET_THINK;
		mes "[Мастер Совершенства]";
		mes "Если вы хотите улучшить ^006400"+.@type$+"^000000, предоставьте ^006400сертификат на "+.@type$+" [Refine Ticket]^000000.";
		mes "До скорого!";
		close;
	}
	mes "[Мастер Совершенства]";
	mes "Выберите, какой ^006400сертификат на "+.@type$+" [Refine Ticket]^000000 вы хотите использовать.";
	next;
	.@menu$ = "";
	for ( .@i = 0; .@i < .@size_cert; ++.@i )
		.@menu$ += getitemname(.@tickets[.@i]) + ":";
	.@select = select(.@menu$)-1;
	.@ticket_lv = .@cert_level[.@select];
	.@ticket_id = .@tickets[.@select];

	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[Мастер Совершенства]";
		mes getitemname(.@ticket_id)+" нет в вашем инвентаре. Возможно вы положили его в хранилище?";
		mes "Пожалуйста, проверьте свои сумки и возвращайтесь.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Мастер Совершенства]";
		mes "^8B4513Этот предмет уже достаточно улучшен.^000000";
		mes "Принесите лучше тот предмет, который действительно нуждается в улучшении.";
		close;
	}
	mes "[Мастер Совершенства]";
	mes "Я собираюсь улучшить ^006400"+getequipname(.@part)+"^8B4513 до +"+.@ticket_lv+" уровня^000000 в замен на ^006400"+getitemname(.@ticket_id)+"^000000.";
	mes "Продолжаем?";
	next;
	if(select("Нет.:Да.") == 1) {
		emotion ET_THINK;
		mes "[Мастер Совершенства]";
		mes "Ты передумал?";
		mes "Хорошо.";
		mes "Возвращайтесь, когда будете уверены в себе.";
		close;
	}
	mes "[Мастер Совершенства]";
	mes "Превосходно.";
	mes "Ваше желание - закон!";
	mes "Воспользуемся моим особым способом...";
	mes ".......ка-бум!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id) < 1) {
		next;
		mes "Ошибка!";
		mes "Что-то пошло не так.";
		close;
	}
	delitem .@ticket_id,1;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
		callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
		mes "[Мастер Совершенства]";
		emotion ET_FRET;
		mes "Wait a second...";
		mes "Do you think I'm stupid?!";
		mes "You switched the item while I wasn't looking! Get out of here!";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[Мастер Совершенства]";
	mes "Хорошо, вот оно ~";
	mes "Вот ваш ^0000FF"+strcharinfo(0)+"^000000!";
	mes "Поздравляю с апгрэйдом.";
	mes "Удачи~!";
	close;
}
