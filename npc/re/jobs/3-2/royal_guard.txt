//===== rAthena Script ======================================= 
//= Royal Guard Job Quest
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Crusader / Paladin -> Royal Guard.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [brAthena]
//= 1.1 Updated script. [Euphy]
//= 1.2 Added GM management function. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//============================================================
//= Translated en > ru [Akaineko]
//============================================================

prt_castle,48,161,3	script	Джентльмен#rg	57,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- Вы перегружены. -";
		mes "- Освободитесь от части груза -";
		mes "- и возвращайтесь. -";
		close;
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "[Джентльмен средних лет]";
		mes "...";
		mes "Что я здесь делаю?..";
		mes "Ах, конечно, у меня задание...";
		next;
		mes "[Джентльмен средних лет]";
		mes "^000099Похоже, я опять в другом теле.^000000";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "[Джентльмен средних лет]";
		mes "О, вы так молоды. Продолжайте тренировки.";
		close;
	}
	if (checkriding() || checkcart() || checkfalcon()) {
		mes "[Джентльмен средних лет]";
		mes "Хм-м, к вам что-то привязано. Имейте совесть, я разговариваю с вами, а не с вашим транспрортным средством!";
		close;
	}
	if (BaseJob != Job_Crusader) {
		mes "[Джентльмен средних лет]";
		mes "Мне нечего вам сказать.";
		close;
	}
	if (job_royal == 0) {
		mes "[Джентльмен средних лет]";
		mes "О чём это я...";
		mes "*бормочет*";
		mes "Последний сегмент, похоже, в Гластхейме, в области 13.......";
		next;
		mes "[Джентльмен средних лет]";
		mes "Хмм?";
		mes "Вам что-то нужно?";
		next;
		select("В первый раз вас вижу.");
		mes "[Джентльмен средних лет]";
		mes "Хе-хе-хе, ну и что с того.";
		next;
		mes "[Джентльмен средних лет]";
		mes "Шли бы вы своей дорогой, "+(Sex?"мой дорогой друг":"леди")+".";
		next;
		select("На рыцаря вы не слишком похожи...");
		mes "[Джентльмен средних лет]";
		mes "Эх, молодость, любопытство... Что вам нужно? Кто вы так"+(Sex?"ой":"ая")+"?";
		next;
		select("Да так, мимо крокодил!:Я здесь на службе!:Кто я так"+(Sex?"ой":"ая")+"? Кто ВЫ такой!");
		mes "[Джентльмен средних лет]";
		mes "Вы пришли специально, чтобы поспорить со мной? Вы вообще знаете, кто перед вами?";
		next;
		if(select("Нет, поэтому и спрашиваю. Кто вы?:Простите, я не хотел"+(Sex?"":"а")+" задеть вас.") == 2) {
			mes "["+strcharinfo(0)+"]";
			mes "Прошу меня простить. Я был"+(Sex?"":"а")+" обязан"+(Sex?"":"а")+" убедиться. Я не узнал"+(Sex?"":"а")+" в вас придворного.";
			next;
			mes "[Джентльмен средних лет]";
			mes "Этого достаточно.";
			mes "И постарайтесь больше не повторять этой ошибки.";
			close;
		}
		mes "["+strcharinfo(0)+"]";
		mes "Я здесь на службе,";
		mes "и я имею право знать,";
		mes "что здесь происходит.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Вы же --";
		mes "явно новое лицо,";
		mes "которое не в курсе";
		mes "заведённых порядков.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Представьтесь, пожалуйста.";
		mes "Тогда и я принесу свои извинения.";
		next;
		mes "[Джентльмен средних лет]";
		mes "Какое упрямство.";
		mes "Что ж, если вы так настаиваете, я, так и быть, представлюсь.";
		next;
		mes "[Джентльмен средних лет]";
		mes "Меня зовут Генрих.";
		mes "Я собираю информацию о следах Его Величества, короля Шмидта.";
		next;
		select("Следы короля Шмидта? Сокровища?");
		mes "[Генрих]";
		mes "Археологов всегда больше всего интересуют именно материальные ценности. Я же ищу следы ^000099Королевской Гвардии^000000 и их сынов, потомков линии Шмидта.";
		next;
		select("Королевская Гвардия?");
		mes "[Генрих]";
		mes "Заинтересованы? К сожалению, я не могу сказать большего. Их дело находится под грифом строгой секретности.";
		next;
		select("Даже мне?");
		mes "[Генрих]";
		mes "Вы же понимаете, я не могу болтать о таких вещах с каждым встречным.";
		next;
		mes "[Генрих]";
		mes "Но вы... Как я устал никому не доверять...";//Но! :) Вам я, так и быть, доверюсь!
		next;
		switch(select("Я хочу знать больше.:Закончить разговор.")) {
		case 1:
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_royal,1;
				setquest 12090;
				next;
			}
			mes "[Генрих]";
			mes "Вас не остановить.";
			next;
			mes "[Генрих]";
			mes "Эту историю непросто разьяснить людям... вашего сорта. Прошу меня извинить.";
			set job_royal,1;
			setquest 12090;
			close;
		case 2:
			mes "[Генрих]";
			mes "Прекрасная мысль.";
			next;
			mes "[Генрих]";
			mes "Забудьте обо всём этом, пусть ваш разум занимается чем-нибудь более полезным.";
			close;
		}
	}
	mes "[Генрих]";
	if (job_royal == 1) {
		mes "С вами недостаточно просто просто попрощаться, "+(Sex?"мой друг":"леди")+".";
		mes "Вы хотели бы поговорить ещё о чём-то?";
		next;
		select("Испытайте меня, если не доверяете.");
		mes "[Генрих]";
		mes "Испытать?";
		mes "Вы сказали, испытать? Х-х-х!";
		next;
		mes "[Генрих]";
		mes (Sex?"Друг мой":"Леди")+", вы так настойчиво интересуетесь моей работой. Скажите, насколько серьёзно это увлечение? Вы просто пытаетесь выглядеть вежливо?";
		next;
		select("Я действительно заинтересован"+(Sex?"":"а")+"!");
		mes "[Генрих]";
		mes "Правда? Хорошо.";
		mes "Возможно, у вас сложилось превратное понятие о том, что такое Королевская Гвардия. Ответьте мне, что, по вашему мнению, является самым главным в службе гвардейца?";
		next;
		select("Решительность и отвага.:Спокойствие и рассудительность.:Мудрость.:Добронамеренность.");
		mes "[Генрих]";
		mes "Не самый плохой выбор.";
		mes "Но остальные три важны не менее, "+(Sex?"мой друг":"леди")+".";
		next;
		mes "[Генрих]";
		mes "Однако согласно историческим документам Гвардии, основными качествами являются аскетизм и выносливость.";
		next;
		mes "[Генрих]";
		mes "Ясно?";
		mes "Аскетизм и выносливость, "+(Sex?"мой юный друг":"юная леди")+". Ваш ответ был несколько иным.";
		next;
		mes "[Генрих]";
		mes "Ваш интерес к этой истории поистине впечатляет, и я глубоко сожалею, что не могу рассказать вам подробности.";
		next;
		mes "[Генрих]";
		mes "Доступ к ним разрешён только людям определённой квалификации.";
		next;
		select("Что мне сделать, чтобы стать одним из них?");//чОткое кол-во символов
		mes "^000099На лице Генриха отражается вся боль и страдание этого мира. Внезапно вы понимаете, как много морщин на этом лице. Когда вам начинает казаться, что Генрих заснул, он открывает глаза, полные усталости, голос его бесцветен.^000000";
		next;
		mes "[Генрих]";
		mes "Вы неисправимы.";
		mes "Так и быть, я испытаю вас на аскетизм и выносливость.";
		next;
		mes "[Генрих]";
		mes "В своё время Его Величество король Шмидт даровал титул воину, который доставил ему сотню священных знаков.";
		next;
		mes "[Генрих]";
		mes "Если вы хотите услышать историю Гвардии, вам придётся проявить своё намерение подобным образом.";
		next;
		if(select("Это нечестное требование.:Я попробую.") == 1) {
			mes "[Генрих]";
			mes "Займитесь чем-нибудь более полезным, действительно.";
			close;
		}
		mes "[Генрих]";
		mes "Договорились. Вообще-то достать сотню священных знаков очень трудная задача. С вас хватит и пылающих сердец.";
		next;
		mes "[Генрих]";
		mes "Принесёте, и я увижу, что вы достаточно выносливы.";
		next;
		mes "[Генрих]";
		mes "Итак, я жду от вас";
		mes "^442200"+ getitemname(7097) +", 100 шт.^000000";//Burning Heart
		set job_royal,2;
		changequest 12090,12091;
		close;
	} else if (job_royal == 2) {
		if (countitem(7097) < 100) {
			mes "Я жду ^442200"+ getitemname(7097) +", 100 шт.^000000.";
			close;
		}
		mes "Вам удалось собрать все сто?";
		mes "Надо же. Большинство не воспринимает меня всерьёз или сдаётся.";
		next;
		mes "[Генрих]";
		mes "Позвольте, я разберусь с тем, что вы принесли.";
		delitem 7097,100; //Burning_Heart
		set job_royal,3;
		changequest 12091,12092;
		close;
	} else if (job_royal == 3) {
		mes "...";
		mes "Хорошо. Вижу, ваше решение искренне. Позвольте рассказать вам историю Королевской Гвардии.";
		next;
		select("Да, пожалуйста.");
		mes "[Генрих]";
		mes "Лучшие среди крестоносцев и паладинов могут претендовать на повышение квалификации и вступление в ряды Королевской Гвардии.";
		next;
		mes "[Генрих]";
		mes "Гвардейцы несут божественный закон и правосудие.";
		next;
		mes "[Генрих]";
		mes "На данный момент у нас нет единого мнения о том, как такой орден мог сохраниться. Однако большинство согласно признать, что их история не является вымыслом.";
		next;
		select("Есть ли документы?");
		mes "[Генрих]";
		mes "Хмм, документы, говорите...";
		next;
		mes "[Генрих]";
		mes "К сожалению, после исчезновения короля Шмидта его Гвардия тоже исчезла, а с ней и информация о её устройстве, культуре, стиле боя.";
		next;
		select("Я не понимаю...");
		mes "[Генрих]";
		mes "Меня обеспокоила судьба гвардейцев прошлого, у которых не было никакой возможности передать знания о своей истории дальше.";
		next;
		mes "[Генрих]";
		mes "Такая потрясающая сила... И в мире её больше нет!";
		next;
		mes "[Генрих]";
		mes "Вот почему я занимаюсь воссозданием этого ордена.";
		next;
		select("Далеко ли вам удалось зайти?");
		mes "[Генрих]";
		mes "К счастью, большинство находок мною уже обработаны и интерпретированы. Осталась последняя зона раскопок.";
		next;
		select("Последний след?");
		mes "[Генрих]";
		mes "В Гвардии было принято наносить знак короля Шмидта на одежду. След этого обычая и есть наша последняя зацепка.";
		next;
		mes "[Генрих]";
		mes "Согласно отчётам из других городов, этот след был найден в монастыре Гластхейма.";
		next;
		mes "[Генрих]";
		mes "Но для меня, как вы понимаете, экспедиция туда практически невозможна. Слишком опасно... Я близок к тому, чтобы отказаться от этой идеи.";
		next;
		mes "[Генрих]";
		mes "И, разумеется, я не могу ответить вам, почему след этого обычая был найден именно там.";
		next;
		if(select("Интересно. Могу ли я отправиться туда?:Да, действительно, слишком опасно.") == 2) {
			mes "[Генрих]";
			mes "Вот видите, это бесполезно.";
			close;
		}
		mes "[Генрих]";
		mes "Вы шутите?";
		mes "Это вряд ли окажется лёгкой прогулкой или уборкой комнаты.";
		next;
		select("Я не шучу. Я абсолютно серьёзно.");
		mes "[Генрих]";
		mes "Ох, судя по вашему тону, у меня просто нет выбора.";
		next;
		mes "[Генрих]";
		mes "Не знаю, поможет это или нет. Вот, возьмите, я записал детали.";
		next;
		mes "^000099Вы получили документ. Откройте окно заданий, чтобы ознакомиться с подробностями.^000000";
		set job_royal,4;
		changequest 12092,12093;
		close;
	} else if (job_royal == 4) {
		if (countitem(6274) == 0) {
			mes "Экспедиция в последнюю зону раскопок уже отправилась, так что не волнуйтесь, если ничего не найдёте.";
			close;
		}
		mes "Нет, это совсем не то, чего я ждал.";
		next;
		mes "[Генрих]";
		mes "Вы меня удивили. Как вам удалось отыскать эту вещь? Это просто невозможно.";
		next;
		mes "[Генрих]";
		mes "Я безмерно вам обязан.";
		mes "Теперь исключительно благодаря вашим стараниям я смогу наконец завершить свои поиски! Чудесно!";
		next;
		mes "^000099Чуть помедлив, Генрих бережно выносит на свет древний металлический щит.^000000";
		next;
		mes "[Генрих]";
		mes "Найден на раскопках. Копатели были уверены, что им пользовался сам король Шмидт. Но это не так.";
		next;
		select("Почему вы так уверены?");
		mes "[Генрих]";
		mes "Конечно, полностью уверенным быть нельзя.";
		mes "Может оказаться, что они были правы. Но согласно первичной идентификации он никогда не был в использовании.";
		next;
		mes "[Генрих]";
		mes "Ценность его прежде всего историческая. И, мне кажется, я знаю, кого наградить такой ценностью.";
		next;
		select("Я могу его взять?");
		mes "[Генрих]";
		mes "Конечно.";
		mes "Он вам пригодится.";
		next;
		mes "^000099Вы получили от Генриха древний щит. На внутренней стороне вы замечаете какие-то буквы.^000000 ";
		next;
		mes "^000099Подробности см. в окне заданий.^000000";
		next;
		mes "[Генрих]";
		mes "Ну что же, вот и славно! А теперь...";
		mes "Мне предстоит изучение того образца ткани, что вы принесли, поэтому прошу меня извинить!..";
		delitem 6274,1; //Saint_Cloth_Piece
		getitem 6275,1; //King_Shield
		set job_royal,5;
		changequest 12093,12094;
		close;
	} else if (job_royal == 5) {
		if (countitem(6275) == 0) {
			mes "Как? Вы потеряли щит? А что это у вас на спине?";
			getitem 6275,1; //King_Shield
			close;
		}
		mes "Наконец-то мои раскопки подойдут к концу! Безмерно вам благодарен.";
		close;
	}
}

glast_01,240,366,5	script	Память о короле Шмидте	844,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- Вы перегружены. -";
		mes "- Освободитесь от части груза -";
		mes "- и возвращайтесь. -";
		close;
	}
	if (ismounting() || checkriding()) {
		mes "Спешьтесь и повторите попытку!";
		close;
	}
	if (BaseJob == Job_Crusader && job_royal == 5) {
		if (countitem(6275) == 0) {
			mes "^000099Похоже, вы умудрились потерять щит. Вернитесь к Генриху и попросите о помощи.^000000";
			close;
		}
		mes "[Память о короле Шмидте]";
		mes "Кто идёт?";
		next;
		select("Я прин"+(Sex?"ёс":"есла")+" реликвию...");
		mes "[Память о короле Шмидте]";
		mes "Моя реликвия?..";
		next;
		mes "[Память о короле Шмидте]";
		mes "Как нехорошо. Ещё одного достойного человека привели вникуда старания бедного Генриха.";
		next;
		select("Вы знакомы?");
		mes "[Память о короле Шмидте]";
		mes "Генрих был моим преданным адъютантом. Впрочем, он и сейчас им является. Сотни лет образцовой службы...";
		next;
		select("Как можно прожить сотни...");
		mes "[Память о короле Шмидте]";
		mes "Магия, вероятно. ";
		mes "Он нашёл способ перемещаться от тела к телу, таким образом захватывая возможность действовать в мире живых.";
		next;
		mes "[Память о короле Шмидте]";
		mes "Он не желает признавать факт моей смерти и так и скитается по миру в поисках новобранцев.";
		next;
		select("Что это за щит?");
		mes "[Память о короле Шмидте]";
		mes "Щит, который дал Генрих -- знак нашего с ним договора и его задачи. Генрих был назначен ответственным за формирование гвардии, отбор людей для неё.";
		next;
		mes "[Память о короле Шмидте]";
		mes "Для Генриха это стало моим последним приказом.";
		next;
		mes "[Память о короле Шмидте]";
		mes "Теперь это дело уже никому не нужно, но Генрих оказался не способен предать волю своего короля... ";
		mes "Не видать ему больше покоя. ";
		mes "Глупый мальчишка...";
		next;
		select("Что же теперь? Я... пойду?");
		mes "[Память о короле Шмидте]";
		mes "Прости, я не могу отослать тебя теперь же.";
		next;
		mes "[Память о короле Шмидте]";
		mes "Договор есть договор. Я вижу твою преданность и решительность.";
		next;
		mes "[Память о короле Шмидте]";
		mes "Пришло время и мне сдержать своё слово. Теперь выбор за тобой.";
		next;
		mes "[Память о короле Шмидте]";
		mes "Я собираюсь принять тебя в ряды Королевской Гвардии.";
		mes "Традиция и история Гвардии, разбитые и забытые, наконец обретут смысл и жизнь.";
		mes "Готов"+(Sex?"":"а")+" ли ты?";
		next;
		if(select("Конечно.:Нет.") == 2) {
			mes "[Память о короле Шмидте]";
			mes "И Генрих может ошибиться.";
			close;
		}
		mes "[Память о короле Шмидте]";
		mes "Сим";
		mes "я принимаю тебя";
		mes "в Королевскую";
		mes "Гвардию.";
		next;
		if(select("Вступаю.:Подождите.") == 2) {
			mes "[Память о короле Шмидте]";
			mes "Даже Генриху свойственна ошибочность суждений.";
			close;
		}
		mes "[Память о короле Шмидте]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
			mes "Сначала посмотри на своё тело!";
			mes "Что это за жалость к себе?";
			next;
			mes "[Память о короле Шмидте]";
			mes "В Гвардии таким не место!";
			close;
		}
		mes "Гвардеец.";
		mes "Достоинство ордена и его славное имя теперь твои.";
		close2;
		specialeffect2 EF_MAPPILLAR;
		progressbar "FFFF00",7;
		specialeffect2 EF_LORD;
		delitem 6275,1; //King_Shield
		erasequest 12094;
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5757,1; //Schmidt_Helm
		getitem 2795,1; //Green_Apple_Ring
		end;
	}
	end;
}

gl_church,173,88,0	script	Следы раскопок#01	844,{
	if (job_royal == 4 && countitem(6274) == 0) {
		progressbar "ffff00",5;
		set .@roy_ran, rand(1,70);
		if (.@roy_ran < 6)
			getitem 6274,1; //Saint_Cloth_Piece
		else if (.@roy_ran > 5 && .@roy_ran < 66)
			getitem 7049,1; //Stone
		else {
			mes "Вы осматриваете камень за камнем, как вдруг пространство вокруг вас будто взрывается. Лучше сменить позицию.";
			disablenpc strnpcinfo(0);
			initnpctimer;
			close;
		}
	}
	mes "Вы видите следы аккуратных раскопок.";
	close;
OnTimer60000:
	enablenpc strnpcinfo(0);
	stopnpctimer;
OnTimer1000:
OnTimer20000:
OnTimer40000:
	specialeffect EF_MAPPILLAR;
	end;
	specialeffect EF_MAPPILLAR;
	end;
	specialeffect EF_MAPPILLAR;
	end;
}
gl_church,158,103,0	duplicate(Следы раскопок#01)	Следы раскопок#02	844
gl_church,139,90,0	duplicate(Следы раскопок#01)	Следы раскопок#03	844
gl_church,126,62,0	duplicate(Следы раскопок#01)	Следы раскопок#04	844
gl_church,148,5,0	duplicate(Следы раскопок#01)	Следы раскопок#05	844
gl_church,185,72,0	duplicate(Следы раскопок#01)	Следы раскопок#06	844
gl_church,185,72,0	duplicate(Следы раскопок#01)	Следы раскопок#07	844
gl_church,211,94,0	duplicate(Следы раскопок#01)	Следы раскопок#08	844
gl_church,185,125,0	duplicate(Следы раскопок#01)	Следы раскопок#09	844
gl_church,143,125,0	duplicate(Следы раскопок#01)	Следы раскопок#10	844
gl_church,105,115,0	duplicate(Следы раскопок#01)	Следы раскопок#11	844

gl_church,158,100,0	script	#Clue of traces	111,4,4,{
	end;
OnTouch_:
	if (job_royal == 4 && countitem(6274) == 0) {
		specialeffect2 EF_FREEZE;
		mes "Вы чувствуете присутствие значительной силы. Пожалуй, стоит осмотреться повнимательнее.";
		viewpoint 1,173,88 ,1, 0xFFFF00;
		viewpoint 1,158,103,2, 0xFFFF00;
		viewpoint 1,139,90 ,3, 0xFFFF00;
		viewpoint 1,126,62 ,4, 0xFFFF00;
		viewpoint 1,148,5  ,5, 0xFFFF00;
		viewpoint 1,185,72 ,6, 0xFFFF00;
		viewpoint 1,211,94 ,7, 0xFFFF00;
		viewpoint 1,185,125,8, 0xFFFF00;
		viewpoint 1,143,125,9, 0xFFFF00;
		viewpoint 1,105,115,10,0xFFFF00;
		close;
	}
	end;
}

sec_in02,12,43,3	script	sorcereryal	57,1,1,{
	callfunc "F_GM_NPC";
	switch(select("Royal Guard:Rune Knight:Sorcerer")) {
	case 1:
		mes "1~5";
		input .@input;
		next;
		if (.@input < 1 || .@input > 5) {
			mes "Set item to adjust the Royal Guard";
			mes "You can only set a number between 1 to 5.";
		} else {
			set job_royal, .@input;
			mes "Completed";
		}
		close;
	case 2:
		mes "1~24";
		input .@input;
		next;
		if (.@input < 1 || .@input > 24) {
			mes "Set item to adjust the Rune Knight";
			mes "You can only set a nbumber between 1 to 24.";
		} else {
			set job_rune_edq, .@input;
			mes "Completed";
		}
		close;
	case 3:
		mes "1~5";
		input .@input;
		next;
		if (.@input < 1 || .@input > 5) {
			mes "Set item to adjust the Sorcerer";
			mes "You can only set a number between 1 to 5.";
		} else {
			set job_soc, .@input;
			mes "Completed";
		}
		close;
	}
}
