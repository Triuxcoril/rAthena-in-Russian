//===== rAthena Script =======================================
//= Old Glast Heim
//===== Description: =========================================
//= Discover the history of events that took place in the
//= Glast Heim castle and how it ended up in ruins.
//===== Changelogs: =================================
//= 1.0 First version. [Euphy, Ziu, Heris]
//=     This is a custom version, and may contain bugs.
//= 1.1 Bug fixes; removed redundant OnInstanceInit scripts.
//= 1.2 Add NPC Hugin's Follower [exneval]
//=     NPC that give access to Glast Heim Nightmare Mode.
//= 1.3 Add some NPCs placeholder. [exneval]
//= 1.4 Update to its official text. [exneval]
//=     Support merchant, enchant, socket features.
//= 1.5 idAthena merge. Removed Hugin's Follower NPCs. [Secretdataz]
//=     Changed `set` calls to direct assignments.
//=     Cleaned up the script.
//=     TODO: Confirm Hugin NPC's code flow.
//= 1.6 Cleaned up the dialogue. [Aleos]
//============================================================
//= Translated en -> ru [Kellait] [Akaineko]
//= Adapted from v1.1 [Akaineko]
// .@md_name$ = "Old Glast Heim"
//============================================================ 

glast_01,204,273,5	script	Хугин	4_M_SAGE_C,{
	if (BaseLevel < 130) {
		mes "[Хугин]";
		mes "Может, заглянете, когда станете постарше?";
		mes "Около 130 уровня.";
		close;
	}
	if (isbegin_quest(12316) == 0) {
		mes "[Хугин]";
		mes "Раньше этот замок выглядел совсем иначе.";
		next;
		mes "[Хугин]";
		mes "Ах, прошу прощения. Я бормочу себе под нос прямо на глазах у прохожих.";
		next;
		mes "[Хугин]";
		mes "Меня зовут Хугин. Я изучаю разломы пространства и времени.";
		next;
		select("Вы нашли что-то подобное здесь?");
		mes "[Хугин]";
		mes "Приходилось ли вам задумываться над историей этого места, Гластхейма?";
		next;
		mes "[Хугин]";
		mes "Одно время здесь правил король Шмитц, известный ныне как тиран и кровопийца.";
		next;
		mes "[Хугин]";
		mes "Но если искать в правильных местах, время открывает настоящую историю.";
		next;
		if (select("Меня не интересует история.","Вам удалось что-то найти?") == 1) {
			mes "[Хугин]";
			mes "Правда? Что ж, если заинтересуетесь, приходите.";
			close;
		}
		mes "[Хугин]";
		mes "Пространственно-временные путешествия действительно возможны, но, признаться, я опасаюсь отправляться в подобные экспедиции один.";
		next;
		mes "[Хугин]";
		mes "Но для вас, полагаю, это не проблема!";
		next;
		mes "[Хугин]";
		mes "Мечтали ли вы когда-нибудь о путешествиях во времени?";
		next;
		if (select("Нет, спасибо.","Конечно!") == 1) {
			mes "[Хугин]";
			mes "Вы серьёзно? Но ведь это открывает такие возможности!..";
			close;
		}
		mes "[Хугин]";
		mes "Я знал, что мы поймём друг друга. Что вы планируете делать?";
		setquest 12316;// Meeting Hugin
		completequest 12316;
	}
	else {
		mes "[Хугин]";
		mes "Хм? Вам кажется, что вы раньше меня где-то видели? Не может этого быть. Чем я могу помочь?";
		if (isbegin_quest(12322) == 1)
			erasequest 12322;
	}
	next;
	switch( checkquest(12317,PLAYTIME) ) {
	case -1:
		.@party_id = getcharid(1);
		.@p_name$ = getpartyname(.@party_id);
		.@md_name$ = "Old Glast Heim";

		if (!instance_check_party(.@party_id)) {
			mes "[Хугин]";
			mes "Вас следует сформировать отряд из 1 и более человека и подойти ко мне ещё раз.";
			close;
		}
		if (getcharid(0) == getpartyleader(.@party_id,2))
			.@menu$ = "Открыть разлом";
		else {
			mes "[Хугин]";
			mes "Встречались ли мы уже? Не думаю. Что вам угодно?";
		}
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		switch( select( .@menu$, "Войти в Старый Гластхейм", "Отмена" ) ) {
		case 1:
			switch( instance_create(.@md_name$) ) {
			case -3:
				dispbottom "Подземелье Старый Гластхейм уже запущено.",0xFFFFFF;
				close;
			case -4:
			case -2:
			case -1:
				mes "Отряд: " + getpartyname( getcharid(1) );
				mes "Командир: " + strcharinfo(0);
				mes "^0000ff" + .@md_name$ + "^000000 - запуск подземелья не удался.";
				close;
			}
			mes "[Хугин]";
			mes "Обратитесь ко мне, когда откроется разлом.";
			close;
		case 2:
			switch( instance_enter(.@md_name$) ) {
			case IE_OTHER:
				mes "[Хугин]";
				mes "Неизвестная ошибка.";
				close;
			case IE_NOINSTANCE:
				mes "[Хугин]";
				mes "Временной разлом ещё не создан.";
				close;
			case IE_NOMEMBER:
				mes "[Хугин]";
				mes "Вы не сможете войти в подземелье, не являясь частью отряда.";
				close;
			case IE_OK:
				mapannounce "glast_01",strcharinfo(0)+", член отряда "+.@p_name$+", входит в Старый Гластхейм.",bc_map,"0x00ff99";
				setquest 12317;// Trace of Time Travel
				// warp "1@gl_k",150,20;
				end;
			}
		case 3:
			close;
		}
	case 0:
	case 1:
		mes "[Хугин]";
		mes "О боже.";
		mes "Вы всё ещё находитесь под влиянием путешествия во времени. Вы не можете продолжать в таком состоянии.";
		next;
		mes "[Хугин]";
		mes "Не стоит пренебрегать своим здоровьем. Отдохните и возвращайтесь позже.";
		close;
	case 2:
		mes "^0000ffДоступ к Гластхейму был сброшен. Теперь вы можете поговорить с Хугином.^000000";
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		erasequest 12317;
		close;
	}
}

// Warps
1@gl_k,96,80,0	warp	#2Control	2,2,1@gl_k,80,80
1@gl_k,90,80,0	warp	#2Control2	2,2,1@gl_k,105,80
1@gl_k,202,79,0	warp	#3Control	2,2,1@gl_k,215,79
1@gl_k,206,79,0	warp	#3Control2	2,2,1@gl_k,195,79
1@gl_k,227,216,0	warp	#4Control	2,2,1@gl_k,215,216
1@gl_k,222,216,0	warp	#4Control2	2,2,1@gl_k,233,216
1@gl_k,150,284,0	warp	#2F Entrance	2,2,2@gl_k,150,46
2@gl_k,149,32,0	warp	#1 Control	2,2,1@gl_k,150,270
2@gl_k,145,123,0	warp	#22 Control	2,2,2@gl_k,126,123
2@gl_k,136,122,0	warp	#22 Control2	2,2,2@gl_k,150,116
2@gl_k,154,101,0	warp	#23 Control	2,2,2@gl_k,174,101
2@gl_k,165,101,0	warp	#23 Control2	2,2,2@gl_k,150,111
2@gl_k,150,163,0	warp	#24 Control	2,2,2@gl_k,150,179
2@gl_k,150,167,0	warp	#24 Control2	2,2,2@gl_k,150,149
1@gl_k,69,168,0	warp	#Secret Room Exit	2,2,1@gl_k,48,168

// Floor 1
//============================================================
1@gl_k,149,41,5	script	Вармунт#0	4_M_BARMUND,{
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Вармунт]";
		mes "Где командир вашего отряда? Мне нужна его помощь.";
		cutin "gl_barmund2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[Вармунт]";
	mes "Эй, это ^0000ffвас^000000 послали помочь мне?";
	npctalk "Вармунт : Эй, это вас послали помочь мне?";
	cutin "gl_barmund1",2;
	next;
	select("Ох. Ну, на самом деле...");
	mes "["+.@player_name$+"]";
	mes "Ох, да, хах, нам сказали встретиться с человеком по имени Вармунт.";
	unittalk .@account_id, .@player_name$ +" : Ох, да, ахах, нам сказали встретиться с человеком по имени Вармунт.";
	next;
	mes "[Вармунт]";
	mes "У нас нет времени. Мы должны рассказать Сэру Генриху о вторжении Химмельмез.";
	npctalk "У нас нет времени. Мы должны рассказать Сэру Генриху о вторжении Химмельмез.";
	cutin "gl_barmund2",2;
	next;
	select("Что за Химмельмез...");
	mes "["+.@player_name$+"]";
	mes "Химмельмез? А это ещё кто?";
	unittalk .@account_id, .@player_name$ +" : Химмельмез? А это ещё кто?";
	next;
	mes "[Вармунт]";
	mes "Вам что, никто не дал основную информацию?";
	npctalk "Вармунт : Вам что, никто не дал основную информацию?";
	cutin "gl_barmund3",2;
	next;
	mes "[Вармунт]";
	mes "Валькирия мёртвых. Она пришла за скрытыми здесь осколками Сердца Имира.";
	npctalk "Вармунт : Валькирия мёртвых. Она пришла за скрытыми здесь осколками Сердца Имира.";
	cutin "gl_barmund2",2;
	next;
	mes "[Вармунт]";
	mes "Ради этого она может уничтожить весь замок.";
	npctalk "Вармунт : Ради этого она может уничтожить весь замок.";
	next;
	mes "[Вармунт]";
	mes "Быстрее! Скажите Сэру Генриху, что Химмельмез здесь. Я быстро исследую чары, которыми она опутала это место.";
	npctalk "Вармунт : Быстрее! Скажите Сэру Генриху, что Химмельмез здесь. Я постараюсь исследовать чары, которыми она опутала это место.";
	close2;
	cutin "",255;
	disablenpc instance_npcname("Вармунт#0");
	enablenpc instance_npcname("Генрих#1");
	enablenpc instance_npcname("Вармунт#1");
	end;
}

1@gl_k,145,104,6	script	Рыцарь Калицбург#1	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "Скоро уже смена караула. Интересно, где же мои сменщики.";
	close;
}

1@gl_k,154,104,3	script	Рыцарь Калицбург#2	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "Вы что-то хотели мне сказать?";
	close;
}

1@gl_k,145,99,6	script	Белый рыцарь#3	4_WHITEKNIGHT,{
	mes "[Белый рыцарь]";
	mes "Вы пришли с Вармунтом? Комендант ждёт.";
	close;
}

1@gl_k,154,99,3	duplicate(Рыцарь Калицбург#1)	Белый рыцарь#4	4_WHITEKNIGHT

1@gl_k,145,94,6	script	Рыцарь Калицбург#5	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "Мне приснился странный сон. Я видела в нём маму... Я за неё волнуюсь.";
	close;
}

1@gl_k,154,94,3	duplicate(Белый рыцарь#3)	Рыцарь Калицбург#6	4_F_KHALITZBURG

1@gl_k,145,89,6	script	Белый рыцарь#7	4_WHITEKNIGHT,{
	mes "[Белый рыцарь]";
	mes "Пожалуйста, держитесь в рамках приличий.";
	close;
}
1@gl_k,154,89,3	duplicate(Белый рыцарь#7)	Белый рыцарь#8	4_WHITEKNIGHT

1@gl_k,145,84,6	script	Рыцарь Калицбург#9	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "Жду приказов.";
	close;
}
1@gl_k,154,84,3	duplicate(Рыцарь Калицбург#9)	Рыцарь Калицбург#10	4_F_KHALITZBURG

1@gl_k,145,79,6	script	Белый рыцарь#11	4_WHITEKNIGHT,{
	mes "[Белый рыцарь]";
	mes "Вы с Вармунтом знакомы? Я слышал, он не самый приятный человек...";
	close;
}
1@gl_k,154,79,3	duplicate(Белый рыцарь#11)	Белый рыцарь#12	4_WHITEKNIGHT

1@gl_k,145,74,6	script	Рыцарь Калицбург#13	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "В замке происходит что-то неладное.";
	close;
}
1@gl_k,154,74,3	duplicate(Рыцарь Калицбург#13)	Рыцарь Калицбург#14	4_F_KHALITZBURG

1@gl_k,145,69,6	script	Белый рыцарь#15	4_WHITEKNIGHT,{
	mes "[Белый рыцарь]";
	mes "Я не люблю болтать на дежурстве.";
	close;
}
1@gl_k,154,69,3	duplicate(Белый рыцарь#15)	Белый рыцарь#16	4_WHITEKNIGHT

1@gl_k,145,64,6	script	Рыцарь Калицбург#17	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "...";
	close;
}
1@gl_k,154,64,3	duplicate(Рыцарь Калицбург#17)	Рыцарь Калицбург#18	4_F_KHALITZBURG

1@gl_k,145,59,6	script	Белый рыцарь#19	4_WHITEKNIGHT,{
	mes "[Белый рыцарь]";
	mes "Могу ли я чем-то помочь?";
	close;
}
1@gl_k,154,59,3	duplicate(Белый рыцарь#19)	Белый рыцарь#20	4_WHITEKNIGHT

1@gl_k,145,54,6	script	Рыцарь Калицбург#21	4_F_KHALITZBURG,{
	mes "[Рыцарь Калицбург]";
	mes "Вам что-то нужно?";
	close;
}
1@gl_k,154,54,3	duplicate(Рыцарь Калицбург#21)	Рыцарь Калицбург#22	4_F_KHALITZBURG

1@gl_k,149,100,6	script	Генрих#1	4_M_HEINRICH,{
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Генрих]";
		mes "Где ваш командир? Мне нужно с ним поговорить.";
		cutin "gl_heinrich2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	cutin "gl_heinrich2",2;
	select("Генрих, насчет замка...");
	mes "["+.@player_name$+"]";
	mes "Генрих, Вы знаете, что сейчас происходит в замке?";
	unittalk .@account_id, .@player_name$ +" : Генрих, Вы знаете, что сейчас происходит в замке?";
	next;
	mes "[Генрих]";
	mes "Вы приключенцы, пришедшие с Вармунтом?";
	npctalk "Генрих : Вы приключенцы, пришедшие с Вармунтом?";
	next;
	mes "[Генрих]";
	mes "Чем я могу Вам помочь? Что-то случилось?";
	npctalk "Генрих : Чем я могу Вам помочь? Что-то случилось?";
	next;
	select("Сердце Имира. Химмельмез...");
	mes "["+.@player_name$+"]";
	mes "Химмельмез, Валькирия мертвых, ищет часть Сердца Имира, спрятанную в замке!";
	unittalk .@account_id, .@player_name$ +" : Химмельмез, Валькирия мертвых, ищет часть Сердца Имира, спрятанную в замке!";
	next;
	mes "[Генрих]";
	mes "Хаха. Отличная шутка. А теперь скажите, что Вам действительно нужно.";
	npctalk "Генрих : Хаха. Отличная шутка. А теперь скажите, что Вам действительно нужно.";
	cutin "gl_heinrich1",2;
	next;
	mes "[Вармунт]";
	mes "Я не шучу, Генрих, сэр. Если мои суждения верны, она скоро появится.";
	npctalk "Вармунт : Я не шучу, Генрих, сэр. Если мои суждения верны, она скоро появится.", instance_npcname("Вармунт#1");
	cutin "gl_barmund2",2;
	next;
	mes "[Вармунт]";
	mes "Я верю своему спутнику. Осколки Сердца должны быть спрятаны в безопасном месте, пока Химмельмез не завладела ими!";
	npctalk "Вармунт : Я верю своему спутнику. Осколки Сердца должны быть спрятаны в безопасном месте, пока Химмельмез не завладела ими!", instance_npcname("Вармунт#1");
	next;
	select("Даже если Вы не верите...");
	mes "["+.@player_name$+"]";
	mes "Даже если Вы не верите, сделайте хоть что-нибудь. У нас мало времени!";
	unittalk .@account_id, .@player_name$ +" : Даже если Вы не верите, сделайте хоть что-нибудь. У нас мало времени!";
	cutin "gl_barmund2",255;
	next;
	mes "[Генрих]";
	mes "Я буду рад, если Вы дадите мне пару минут. Но короля сейчас нет.";
	npctalk "Генрих : Я буду рад, если Вы дадите мне пару минут. Но короля сейчас нет.";
	cutin "gl_heinrich1",2;
	next;
	mes "[Генрих]";
	mes "Мы не можем пойти на риск и переместить Сердце только потому, что так говорит какой-то прохожий.";
	npctalk "Мы не можем пойти на риск и переместить Сердце только потому, что так говорит какой-то прохожий.";
	close2;
	disablenpc instance_npcname("Генрих#1");
	enablenpc instance_npcname("Генрих#2");
	enablenpc instance_npcname("Химмельмез#1");
	donpcevent instance_npcname("#talkinstance1") + "::OnEnable";
	cutin "",255;
	end;
}

1@gl_k,152,97,3	script	Вармунт#1	4_M_BARMUND,{ end; }
1@gl_k,149,97,5	duplicate(Вармунт#1)	Генрих#2	4_M_HEINRICH
1@gl_k,149,100,5	duplicate(Вармунт#1)	Генрих#3	4_M_HEINRICH
1@gl_k,149,89,1	duplicate(Вармунт#1)	Химмельмез#1	4_F_HIMEL

// Control Timer
//============================================================
1@gl_k,1,1,0	script	#talkinstance1	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	mapannounce 'map_name$[0], "???? : Оххохохохо~!", bc_map,0xFFFF00,FW_NORMAL,18;
	initnpctimer;
	'npc_himelmez1$ = instance_npcname("Химмельмез#1");
	'npc_varmundt1$ = instance_npcname("Вармунт#1");
	'npc_heinrich2$ = instance_npcname("Генрих#2");
	'npc_heinrich3$ = instance_npcname("Генрих#3");
	end;
OnTimer1500:
	npctalk "Генрих : Кто здесь?!", 'npc_heinrich2$;
	end;
OnTimer4500:
	npctalk "Химмельмез : Ох~ Я прервала ваш разговор? Не ждали?", 'npc_himelmez1$;
	end;
OnTimer10000:
	npctalk "Вармунт : Химмельмез!! Чёрт возьми!", 'npc_varmundt1$;
	end;
OnTimer17500:
	npctalk "Химмельмез : Я - Лиза Канн Химмельмез. Меня называют Валькирией мёртвых, владычицей Дуллахана.", 'npc_himelmez1$;
	end;
OnTimer22000:
	npctalk "Генрих : Владычица мёртвых? Без сомнения, занятная история. Пора бы угостить дам чаем. Но к сожалению, я не...", 'npc_heinrich2$;
	end;
OnTimer28500:
	npctalk "Химмельмез : Посмотрим, как вы ответите, когда узнаете, где сейчас ваш король.", 'npc_himelmez1$;
	end;
OnTimer36000:
	npctalk "Генрих : Что ты сказала?", 'npc_heinrich2$;
	end;
OnTimer41000:
	npctalk "Химмельмез : Хохохо, что, мне удалось тебя заинтересовать? Это ведь ты столь загадочный мужчина.", 'npc_himelmez1$;
	end;
OnTimer47000:
	npctalk "Химмельмез : Ты не желаешь престола, покорно дожидаешься его возвращения. Это глупо. Но ты так предан, это мне нравится.", 'npc_himelmez1$;
	end;
OnTimer54000:
	npctalk "Химмельмез : Даже заставляешь меня жалеть, что мы встретились при таких обстоятельствах. Жаль, что всё именно так.", 'npc_himelmez1$;
	end;
OnTimer59000:
	npctalk "Генрих : Мой король отбыл по приглашению королевской семьи Рун-Мидгарда и ещё не вернулся.", 'npc_heinrich2$;
	end;
OnTimer66500:
	npctalk "Генрих : Не пытайся обхитрить меня. Говори, что с ним?!", 'npc_heinrich2$;
	end;
OnTimer71500:
	npctalk "Химмельмез : Я бы с радостью поболтала об этом, но, видите ли, у меня есть кое-какие дела~", 'npc_himelmez1$;
	end;
OnTimer78000:
	npctalk "Химмельмез : Мне нужно завершить их. Почему бы вам пока не разобраться с моими подданными~", 'npc_himelmez1$;
	end;
OnTimer84500:
	npctalk "Генрих : Проклятье! Она поняла, где находятся осколки Сердца.", 'npc_heinrich2$;
	disablenpc 'npc_himelmez1$;
	end;
OnTimer90500:
	npctalk "Генрих : Калицбурги и Белые рыцари, за мной...", 'npc_heinrich2$;
	end;
OnTimer92000:
	for (.@i = 1; .@i <= 20; .@i += 4) {
		hideonnpc instance_npcname("Рыцарь Калицбург#" + .@i);
		hideonnpc instance_npcname("Рыцарь Калицбург#" + (.@i+1));
		hideonnpc instance_npcname("Белый рыцарь#" + (.@i+2));
		hideonnpc instance_npcname("Белый рыцарь#" + (.@i+3));
	}
	hideonnpc instance_npcname("Рыцарь Калицбург#21");
	hideonnpc instance_npcname("Рыцарь Калицбург#22");

	.@label$ = instance_npcname("#talkinstance1") + "::OnMyMobDead";
	monster 'map_name$[0],145,59,"Рыцарь Бездны",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
	monster 'map_name$[0],154,59,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],145,69,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],154,69,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],145,79,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],154,79,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],145,89,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],154,89,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],145,99,"Рыцарь Бездны",2470,1,.@label$;
	monster 'map_name$[0],154,99,"Рыцарь Бездны",2470,1,.@label$;

	monster 'map_name$[0],145,54,"Калицбург",2471,1,.@label$;// G_MG_KHALITZBURG
	unittalk $@mobid[0],"Воды... Кто-нибудь, дайте воды...";
	monster 'map_name$[0],154,54,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"*Хрипит*...";
	monster 'map_name$[0],145,64,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Сэр Генрих! Спасите...";
	monster 'map_name$[0],154,64,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Сестра...";
	monster 'map_name$[0],145,74,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Помогите, не бросайте меня!";
	monster 'map_name$[0],154,74,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Аааааррррккххх~";
	monster 'map_name$[0],145,84,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Так хочется пить.";
	monster 'map_name$[0],154,84,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Ох... Нет... Я не могу умереть...";
	monster 'map_name$[0],145,94,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Так болит живот, аххх...";
	monster 'map_name$[0],154,94,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Кто-нибудь...";
	monster 'map_name$[0],145,104,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Ахх... Моё тело...";
	monster 'map_name$[0],154,104,"Калицбург",2471,1,.@label$;
	unittalk $@mobid[0],"Горло просто пылает...";
	end;
OnTimer94000:
	npctalk "Генрих : Невероятно. Мои люди... Нет, не может быть!", 'npc_heinrich2$;
	end;
OnTimer96500:
	npctalk "Вармунт : Генрих, сэр, они чудовища. Ваш долг отправить их на покой.", 'npc_varmundt1$;
	end;
OnTimer100000:
	npctalk "Генрих : Простите...", 'npc_heinrich2$;
	end;
OnTimer103000:
	npctalk "Генрих : Мои рыцари! Простите меня!", 'npc_heinrich2$;
	end;
OnTimer106000:
	mapannounce 'map_name$[0], "Сэр Генрих : Смерть чудовищам!", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109000:
	mapannounce 'map_name$[0], "Сэр Генрих : Возвращайтесь во тьму!", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#1");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#2");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#1");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#2");
	end;
OnTimer110000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#3");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#4");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#5");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#6");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#3");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#4");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#5");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#6");
	end;
OnTimer110500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#7");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#8");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#9");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#10");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#7");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#8");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#9");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#10");
	end;
OnTimer111000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#11");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#12");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#13");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#14");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#11");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#12");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#13");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#14");
	end;
OnTimer111500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#15");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#16");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#17");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#18");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#15");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#16");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#17");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#18");
	end;
OnTimer112000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#19");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Белый рыцарь#20");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#21");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("Рыцарь Калицбург#22");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#19");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Белый рыцарь#20");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#21");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("Рыцарь Калицбург#22");
	disablenpc 'npc_heinrich2$;
	enablenpc 'npc_heinrich3$;
	killmonster 'map_name$[0], instance_npcname("#talkinstance1") + "::OnMyMobDead";
	end;
OnTimer112500:
	npctalk "Генрих : Я своими руками перебил своих же людей!", 'npc_heinrich3$;
	end;
OnTimer115500:
	npctalk "Вармунт : Генрих, сэр! У нас нет времени!", 'npc_varmundt1$;
	end;
OnTimer118500:
	npctalk "Вармунт : Если мы поспешим, ещё останется шанс!", 'npc_varmundt1$;
	end;
OnTimer121500:
	npctalk "Генрих : ... Вармунт прав. Не время для скорби.", 'npc_heinrich3$;
	end;
OnTimer124500:
	npctalk "Генрих : Вы, подойдите и следуйте моим приказам.", 'npc_heinrich3$;
	end;
OnTimer127500:
	npctalk "Генрих : Химмельмез превращает живых людей в чудовищ.", 'npc_heinrich3$;
	end;
OnTimer130500:
	npctalk "Генрих : Я не знаю, есть ли здесь другие выжившие.", 'npc_heinrich3$;
	end;
OnTimer134500:
	npctalk "Генрих : Но если хоть кто-то пережил это злодеяние, пожалуйста, спасите их.", 'npc_heinrich3$;
	end;
OnTimer138500:
	npctalk "Генрих : Мы с Вармунтом начнём преследование.", 'npc_heinrich3$;
	end;
OnTimer143500:
	npctalk "Генрих : Хорошо, Вармунт. Давайте найдём эту Химмельмез.", 'npc_heinrich3$;
	end;
OnTimer147500:
	disablenpc 'npc_heinrich3$;
	end;
OnTimer148500:
	stopnpctimer;
	donpcevent instance_npcname("#ghmemorialmob01") + "::OnStart";

	disablenpc 'npc_varmundt1$;
	disablenpc instance_npcname("#talkinstance1");
	'npc_himelmez1$ = 'npc_varmundt1$ = 'npc_heinrich2$ = 'npc_heinrich3$ = "";
	end;
OnMyMobDead:
	end;
}

// Sector 1 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob01	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob01");
	enablenpc instance_npcname("#2Control");
	enablenpc instance_npcname("#2Control2");
	mapannounce 'map_name$[0], "Проход во вторую зону появился в направлении на 9 часов.", bc_map,0xFFFF00;

	// note: x17 MG_GHOUL / x17 MG_ZOMBIE / x17 MG_WRAITH
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	areamonster 'map_name$[0],76,99,87,10,"Страдающий камергер",2466,14,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"Искажённый монах",2465,17,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"Страдающий камергер",2466,3,.@label$;
	areamonster 'map_name$[0],32,75,51,58,"Продажный слуга",2464,5,.@label$;
	areamonster 'map_name$[0],45,84,6,137,"Продажный слуга",2464,12,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	.@mob_dead_num = 51 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 35) {
		mapannounce 'map_name$[0], "Проклятие Химмельмез слабеет. Найдите выживших!", bc_map,0xFFFFFF;
		killmonster 'map_name$[0], .@label$;
		enablenpc instance_npcname("Прислужник Домун#1");
		disablenpc instance_npcname("#ghmemorialmob01");
	}
	end;
}

1@gl_k,17,51,3	script	Прислужник Домун#1	4_M_KID1,{
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Прислужник Домун]";
		mes "Помогите! Помогите!!!";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[Прислужник Домун]";
	mes "Помогите! Помогите!!!";
	npctalk "Помогите! Помогите!!!";
	next;
	select("Успокойся! Где остальные выжившие?");
	mes "[" + .@player_name$ + "]";
	mes "Успокойся! Где остальные выжившие?";
	unittalk .@account_id, .@player_name$ + " : Успокойся! Где остальные выжившие?";
	next;
	mes "[Прислужник Домун]";
	mes "Управляющий... и монахи... Они стали монстрами. Я не смог ничего сделать.";
	npctalk "Прислужник Домун: Управляющий... и монахи... Они стали монстрами. Я не смог ничего сделать.";
	next;
	mes "[Прислужник Домун]";
	mes "Я просто спрятался... И ничего, ничего не сделал...";
	npctalk "Прислужник Домун: Я просто спрятался... И ничего, ничего не сделал...";
	next;
	select("Очнись!");
	mes "[" + .@player_name$ + "]";
	mes "Дитя, очнись! Отправляйся к выходу по восточному коридору! Путь свободен!";
	unittalk .@account_id, .@player_name$ + " : Дитя, очнись! Отправляйся к выходу по восточному коридору! Путь свободен!";
	next;
	mes "[Прислужник Домун]";
	mes "По восточному коридору? Один? Как?";
	npctalk "Прислужник Домун: По восточному коридору? Один? Как?";
	next;
	select("Я дам тебе оружие.");
	mes "[" + .@player_name$ + "]";
	mes "Вот оружие. Увидишь монстра - закрывай глаза и бей.";
	unittalk .@account_id, .@player_name$ + " : Вот оружие. Увидишь монстра - закрывай глаза и бей.";
	next;
	mes "[Прислужник Домун]";
	mes "Ох, хорошо... Я постараюсь.";
	npctalk "Прислужник Домун: Ох, хорошо... Я постараюсь.";
	disablenpc instance_npcname("Прислужник Домун#1");
	donpcevent instance_npcname("#ghmemorialmob02") + "::OnStart";
	close;
}

// Sector 2 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob02	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob02");
	enablenpc instance_npcname("#3Control");
	enablenpc instance_npcname("#3Control2");
	for ( .@i = 1; .@i <= 26; .@i++ )
		enablenpc instance_npcname("Мертвец#" + .@i);
	mapannounce 'map_name$[0], "В направлении на 3 часа открылся проход.", bc_map,0xFFFF00;

	// note: x34 MG_ZOMBIE / x35 MG_GHOUL
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	areamonster 'map_name$[0],236,12,288,52,"Поруганная оружейница",2466,18,.@label$;// MG_GHOUL
	areamonster 'map_name$[0],236,12,288,52,"Разлагающийся кузнец",2464,17,.@label$;// MG_ZOMBIE
	areamonster 'map_name$[0],242,71,286,145,"Поруганная оружейница",2466,17,.@label$;
	areamonster 'map_name$[0],242,71,286,145,"Разлагающийся кузнец",2464,17,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	.@mob_dead_num = 69 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 56) {
		mapannounce 'map_name$[0], "Проклятие Химмельмез слабеет. Найдите выживших!", bc_map,0xFFFFFF;
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("Холлгренн#1");
		disablenpc instance_npcname("#ghmemorialmob02");
	}
	end;
}

1@gl_k,291,145,3	script	Холлгренн#1	4_F_JOB_BLACKSMITH,{
	if (getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Разрушительница Холлгренн]";
		mes "Ты точно человек?";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[Разрушительница Холлгренн]";
	mes "Аааа!! Умри!!!";
	npctalk "Разрушительница Холлгренн : Аааа!! Умри!!!";
	specialeffect EF_CRASHEARTH;
	next;
	select("Успокойтесь! Я не монстр!");
	mes "[" + .@player_name$ + "]";
	mes "Не волнуйтесь! Мэм, вы одна? Нет других выживших?";
	unittalk .@account_id, .@player_name$ +" : Не волнуйтесь! Мэм, вы одна? Нет других выживших?";
	next;
	mes "[Разрушительница Холлгренн]";
	mes "Нет, я единственная.";
	npctalk "Разрушительница Холлгренн : Нет, я единственная.";
	next;
	select("Здесь очень опасно.");
	mes "["+.@player_name$+"]";
	mes "Здесь очень опасно. Вы знаете, где центральный проход? Можете двигаться? Нужно перебраться в безопасное место.";
	unittalk .@account_id, .@player_name$ +" : Здесь очень опасно. Вы знаете, где центральный проход? Можете двигаться? Нужно перебраться в безопасное место.";
	next;
	mes "[Разрушительница Холлгренн]";
	mes "Да, я смогу, ради своего ребёнка.";
	npctalk "Разрушительница Холлгренн : Да, я смогу, ради своего ребёнка.";
	next;
	select("Уцелеть в дороге...");
	mes "["+.@player_name$+"]";
	mes "Вы с ребёнком выберетесь в целости. Но мне жаль, что я не могу больше ничем помочь.";
	unittalk .@account_id, .@player_name$ +" : Вы с ребёнком выберетесь в целости. Но мне жаль, что я не могу больше ничем помочь.";
	next;
	mes "[Разрушительница Холлгренн]";
	mes "Всё в порядке, спасибо, что помогли нам. Я смогу добраться одна. И... желаю удачи.";
	npctalk "Разрушительница Холлгренн : Всё в порядке, спасибо, что помогли нам. Я смогу добраться одна. И... желаю удачи.";
	disablenpc instance_npcname("Холлгренн#1");
	donpcevent instance_npcname("#ghmemorialmob03") + "::OnEnable";
	close;
}

// Tramp Mobs
//============================================================
1@gl_k,221,82,3	script	Мертвец#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, UNITTYPE_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Червь",2467,.@mobs;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,233,123,4	duplicate(Мертвец#1)	Мертвец#2	4_M_DIEMAN,5,5
1@gl_k,258,150,2	duplicate(Мертвец#1)	Мертвец#3	4_M_DIEMAN,5,5
1@gl_k,255,157,5	duplicate(Мертвец#1)	Мертвец#4	4_M_DIEMAN,5,5
1@gl_k,280,167,4	duplicate(Мертвец#1)	Мертвец#5	4_M_DIEMAN,5,5
1@gl_k,293,161,2	duplicate(Мертвец#1)	Мертвец#6	4_M_DIEMAN,5,5
1@gl_k,249,101,3	duplicate(Мертвец#1)	Мертвец#7	4_M_DIEMAN,5,5
1@gl_k,241,86,6	duplicate(Мертвец#1)	Мертвец#8	4_M_DIEMAN,5,5
1@gl_k,246,62,0	duplicate(Мертвец#1)	Мертвец#9	4_M_DIEMAN,5,5
1@gl_k,240,43,7	duplicate(Мертвец#1)	Мертвец#10	4_M_DIEMAN,5,5
1@gl_k,271,19,1	duplicate(Мертвец#1)	Мертвец#11	4_M_DIEMAN,5,5
1@gl_k,282,48,5	duplicate(Мертвец#1)	Мертвец#12	4_M_DIEMAN,5,5
1@gl_k,285,81,7	duplicate(Мертвец#1)	Мертвец#13	4_M_DIEMAN,5,5
1@gl_k,276,106,0	duplicate(Мертвец#1)	Мертвец#14	4_M_DIEMAN,5,5
1@gl_k,261,164,0	duplicate(Мертвец#1)	Мертвец#15	4_M_DIEMAN,5,5
1@gl_k,269,173,0	duplicate(Мертвец#1)	Мертвец#16	4_M_DIEMAN,5,5
1@gl_k,252,120,0	duplicate(Мертвец#1)	Мертвец#17	4_M_DIEMAN,5,5
1@gl_k,213,63,0	duplicate(Мертвец#1)	Мертвец#18	4_M_DIEMAN,5,5
1@gl_k,222,39,0	duplicate(Мертвец#1)	Мертвец#19	4_M_DIEMAN,5,5
1@gl_k,214,27,0	duplicate(Мертвец#1)	Мертвец#20	4_M_DIEMAN,5,5
1@gl_k,223,17,1	duplicate(Мертвец#1)	Мертвец#21	4_M_DIEMAN,5,5
1@gl_k,230,50,2	duplicate(Мертвец#1)	Мертвец#22	4_M_DIEMAN,5,5
1@gl_k,235,16,3	duplicate(Мертвец#1)	Мертвец#23	4_M_DIEMAN,5,5
1@gl_k,226,96,4	duplicate(Мертвец#1)	Мертвец#24	4_M_DIEMAN,5,5
1@gl_k,222,119,5	duplicate(Мертвец#1)	Мертвец#25	4_M_DIEMAN,5,5
1@gl_k,251,20,6	duplicate(Мертвец#1)	Мертвец#26	4_M_DIEMAN,5,5

// Sector 3 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob03	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob03");
	enablenpc instance_npcname("#4Control");
	enablenpc instance_npcname("#4Control2");
	mapannounce 'map_name$[0], "В северо-западной стороне открылся проход.", bc_map,0xFFFF00;

	// note: x50 MG_RAYDRIC / x50 MG_RAYDRIC_ARCHER
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	areamonster 'map_name$[0],16,186,43,280,"Поруганная лучница",2469,25,.@label$;
	areamonster 'map_name$[0],44,191,89,225,"Голодный страж",2468,25,.@label$;
	areamonster 'map_name$[0],115,215,188,273,"Сгнивший страж",2468,20,.@label$;
	areamonster 'map_name$[0],108,232,193,281,"Сгнивший страж",2468,5,.@label$;
	areamonster 'map_name$[0],158,236,230,250,"Заблудший стрелок",2469,12,.@label$;
	areamonster 'map_name$[0],65,231,155,256,"Заблудший стрелок",2469,13,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	.@mob_dead_num = 100 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 85) {
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("#Mimelon");
		enablenpc instance_npcname("Химмельмез#2");
		enablenpc instance_npcname("Вармунт#2");
		enablenpc instance_npcname("Генрих#4");
		disablenpc instance_npcname("#ghmemorialmob03");
		mapannounce 'map_name$[0], "Химмельмез : Неплохо. Я ожидала, что к этому моменту мои создания уже вас сожрут~", bc_map,0xFFFFFF;
		initnpctimer;
	}
	end;
OnTimer5000:
	mapannounce 'map_name$[0], "Химмельмез : Я буду ждать вас в северном крыле. Посмотрим, насколько вы удачливы.", bc_map,0xFFFFFF;
	stopnpctimer;
	end;
}

// 1st MVP
//============================================================
1@gl_k,144,258,6	script	Генрих#4	4_M_HEINRICH,{
	mes "[Генрих]";
	mes "Химмельмез... Я никогда не прощу тебе то, что ты сотворила с моими людьми.";
	cutin "gl_heinrich1",2;
	close3;
}

// note: never hidden
1@gl_k,156,258,3	script	Вармунт#2	4_M_BARMUND,{
	cutin "gl_barmund1",2;
	mes "[Вармунт]";
	if (checkquest(12318,HUNTING) != 2) {
		mes "Вот как всё обернулось. Но возможно ли остановить лорда времени?!";
		mes "Ох, похоже, в северной части открылся проход.";
		close3;
	}
	erasequest 12318;
	if (isbegin_quest(12319) == 0)
		setquest 12319;// Amdarais Hunt
	if (isbegin_quest(12320) == 0) {
		setquest 12320;// Time Traveler
		completequest 12320;
		getexp 250000,250000;
	}
	mes "Я нашёл предмет с очень необычной аурой, он может оказаться полезным.";
	getitem 6607,1;// Temporal_Crystal
	getitem 6608,1;// Coagulated_Spell
	close3;
}

1@gl_k,150,257,3	script	Химмельмез#2	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Химмельмез]";
	mes "Не смотри на меня так нервно. Скоро всё кончится...";
	close3;
}

1@gl_k,150,257,0	script	#Mimelon	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Mimelon");
	initnpctimer;
	'npc_himelmez2$ = instance_npcname("Химмельмез#2");
	'npc_heinrich4$ = instance_npcname("Генрих#4");
	end;
OnTimer3000:
	npctalk "Химмельмез : Вы ужасно удачливы, раз смогли добраться сюда.", 'npc_himelmez2$;
	end;
OnTimer6000:
	npctalk "Химмельмез : Но это не имеет значения.", 'npc_himelmez2$;
	end;
OnTimer9000:
	npctalk "Химмельмез : Сейчас все вы умрёте.", 'npc_himelmez2$;
	end;
OnTimer12000:
	npctalk "Генрих : Химмельмез! Я больше не позволю тебе сделать ни единого шага!", 'npc_heinrich4$;
	end;
OnTimer15000:
	npctalk "Химмельмез : Хахаха, о себе лучше беспокойтесь. Вы что, подумали, что я пришла одна?", 'npc_himelmez2$;
	end;
OnTimer18000:
	npctalk "Вармунт : Сэр Генрих! Я ощущаю, как приближается нечто с невероятной, мощной силой!", instance_npcname("Вармунт#2");
	end;
OnTimer21000:
	npctalk "Генрих : Что... это такое?!", 'npc_heinrich4$;
	end;
OnTimer24000:
	npctalk "Химмельмез : Всего лишь моя новая игрушка, чтобы вам не было скучно, пока я продолжу свой путь.", 'npc_himelmez2$;
	end;
OnTimer27000:
	npctalk "Химмельмез : Искренне~ надеюсь, что у меня появится возможность снова увидеться с тобой, Генрих.", 'npc_himelmez2$;
	end;
OnTimer28000:
	disablenpc 'npc_himelmez2$;
	end;
OnTimer31000:
	npctalk "Генрих : Сэр Вармунт! Помогите путешественникам, а я продолжу преследование!", 'npc_heinrich4$;
	end;
OnTimer32000:
	disablenpc 'npc_heinrich4$;
	end;
OnTimer35000:
	mapannounce 'map_name$[0], "Из глубин святилища доносится жуткое эхо, и оно становится всё громче.", bc_map,0xFFFFFF;
	end;
OnTimer38000:
	donpcevent instance_npcname("#ghmemorialmob04") + "::OnStart";
	stopnpctimer;
	'npc_himelmez2$ = 'npc_heinrich4$ = "";
	end;
}

1@gl_k,1,1,0	script	#ghmemorialmob04	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob04");
	monster 'map_name$[0],150,259,"Искажённые души",2475,1, instance_npcname("#ghmemorialmob04") + "::OnMyMobDead";// MG_CORRUPTION_ROOT
	unittalk $@mobid[0],"*Рычит*~~~";
	end;
OnMyMobDead:
	if (mobcount('map_name$[0], instance_npcname("#ghmemorialmob04") + "::OnMyMobDead") < 1) {
		mapannounce 'map_name$[0], "Проход на второй этаж появился в направлении на 12 часов.", bc_map,0xFFFF00;
		npctalk "Вармунт : Я подобрал интересную вещь с этого монстра. Если кому-то понадобится - скажите мне.", instance_npcname("Вармунт#2");

		enablenpc instance_npcname("#2F Entrance");
		enablenpc instance_npcname("#1 Control");
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		enablenpc instance_npcname("#Servanton");
		enablenpc instance_npcname("Генрих#21");
		enablenpc instance_npcname("Вармунт#21");
		donpcevent instance_npcname("#ghmemorialmob05") + "::OnStart";
		disablenpc instance_npcname("#ghmemorialmob04");
	}
	end;
}

// Floor 2
//============================================================
2@gl_k,148,67,1	script	Генрих#21	4_M_HEINRICH,{
	mes "[Генрих]";
	mes "Здесь повсюду её блокирующие заклинания...";
	cutin "gl_heinrich1",2;
	close3;
}

2@gl_k,151,71,7	script	Вармунт#21	4_M_BARMUND,{
	mes "[Вармунт]";
	mes "Почему я снова делаю всё это? Я столько раз видел это место во снах.";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,150,66,0	script	#Servanton_effect	HIDDEN_WARP_NPC,{
	end;
OnEffect:
	specialeffect EF_LORD;
	end;
}

// note: this part can be skipped
2@gl_k,150,66,0	script	#Servanton	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Servanton");
	initnpctimer;
	'npc_heinrich$ = instance_npcname("Генрих#21");
	'npc_varmundt$ = instance_npcname("Вармунт#21");
	end;
OnTimer3000:
	npctalk "Генрих : Этого никогда не было в замке!", 'npc_heinrich$;
	end;
OnTimer6000:
	npctalk "Вармунт : Её заклинания полностью заблокировали все проходы.", 'npc_varmundt$;
	end;
OnTimer9000:
	npctalk "Генрих : Оружием эту силу не развеять. Я пытался.", 'npc_heinrich$;
	end;
OnTimer12000:
	npctalk "Вармунт : Отойдите-ка на секунду. Я попытаюсь обезвредить заклятье с помощью магии.", 'npc_varmundt$;
	end;
OnTimer15000:
	specialeffect EF_LORD,AREA, instance_npcname("#Servanton_effect");
	disablenpc instance_npcname("#Servanton_effect");
	end;
OnTimer18000:
	npctalk "Вармунт : Кажется, мне удалось пробиться.", 'npc_varmundt$;
	end;
OnTimer21000:
	npctalk "Вармунт : Никогда прежде не видел такого.", 'npc_varmundt$;
	end;
OnTimer24000:
	npctalk "Вармунт : Химмельмез не нужна печать на теле жертвы, чтобы превратить её в нежить.", 'npc_varmundt$;
	end;
OnTimer27000:
	npctalk "Вармунт : Она использует зачарованные камни. Похоже, с помощью них и поддерживаются заклинания.", 'npc_varmundt$;
	end;
OnTimer30000:
	npctalk "Вармунт : Чтобы снять заклинание, мы должны убить тех, кто их хранит.", 'npc_varmundt$;
	end;
OnTimer33000:
	npctalk "Вармунт : Но мы не знаем, у кого они. Придётся проверять всех.", 'npc_varmundt$;
	end;
OnTimer36000:
	npctalk "Генрих : Это непростительно.", 'npc_heinrich$;
	end;
OnTimer39000:
	npctalk "Генрих :  Я уже организовал геноцид. Но сколько ещё жертв несёт будущее...", 'npc_heinrich$;
	end;
OnTimer42000:
	npctalk "Вармунт : Комендант...", 'npc_varmundt$;
	end;
OnTimer45000:
	npctalk "Вармунт : Комендант! Будьте тверды.", 'npc_varmundt$;
	end;
OnTimer48000:
	npctalk "Вармунт : Наш враг не человек, и даже не люди.", 'npc_varmundt$;
	end;
OnTimer51000:
	npctalk "Вармунт : Даже если вы не готовы признавать это, их не вернуть.", 'npc_varmundt$;
	end;
OnTimer54000:
	npctalk "Генрих : ...", 'npc_heinrich$;
	end;
OnTimer57000:
	npctalk "Вармунт : Пора приступать.", 'npc_varmundt$;
	end;
OnTimer60000:
	npctalk "Вармунт : Вы очень помогли нам. Можете сделать привал, но постарайтесь не отставать.", 'npc_varmundt$;
	end;
OnTimer63000:
	npctalk "Вармунт : Нам предстоит тяжёлая битва.", 'npc_varmundt$;
	end;
OnTimer66000:
	npctalk "Вармунт : Нам пора выдвигаться, сэр.", 'npc_varmundt$;
	end;
OnTimer69000:
	mapannounce 'map_name$[1], "Проход в первую зону появился по направлению на 9 часов. Он расположен в центральном коридоре.", bc_map,0xFFFF00;
	disablenpc 'npc_varmundt$;
	disablenpc 'npc_heinrich$;
	stopnpctimer;
	'npc_varmundt$ = 'npc_heinrich$ = "";
	end;
}

// Spots
// Note: timer is the only condition for them to respawn
2@gl_k,118,141,0	script	#ogh_2-1	HIDDEN_WARP_NPC,20,20,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@hidden_name$ = strnpcinfo(2);
	.@event_type = atoi( charat(.@hidden_name$,4) );
	if (.@event_type == 2)
		.@label$ = instance_npcname("#ghmemorialmob05") + "::OnMyMobDead";
	else
		.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	getmapxy .@map$,.@x,.@y, UNITTYPE_NPC;
	monster .@map$,.@x,.@y, "Сгнивший страж", 2468,1, .@label$;
	monster .@map$,.@x,.@y, "Меткий стрелок", 2469,1, .@label$;
	monster .@map$,.@x,.@y, "Падший рыцарь", 2470,1, .@label$;
	monster .@map$,.@x,.@y, "Страдающий Калицбург", 2471,1, .@label$;
	monster .@map$,.@x,.@y, "Запятнанный рыцарь", 2472,1, .@label$;
	initnpctimer;
	end;
OnTimer30000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
OnStop:
	disablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,128,81,0	duplicate(#ogh_2-1)	#ogh_2-2	HIDDEN_WARP_NPC,20,20
2@gl_k,131,54,0	duplicate(#ogh_2-1)	#ogh_2-3	HIDDEN_WARP_NPC,20,20
2@gl_k,89,48,0	duplicate(#ogh_2-1)	#ogh_2-4	HIDDEN_WARP_NPC,20,20
2@gl_k,64,117,0	duplicate(#ogh_2-1)	#ogh_2-5	HIDDEN_WARP_NPC,20,20
2@gl_k,62,82,0	duplicate(#ogh_2-1)	#ogh_2-6	HIDDEN_WARP_NPC,20,20
2@gl_k,38,138,0	duplicate(#ogh_2-1)	#ogh_2-7	HIDDEN_WARP_NPC,20,20

2@gl_k,171,120,0	duplicate(#ogh_2-1)	#ogh_3-1	HIDDEN_WARP_NPC,20,20
2@gl_k,232,133,0	duplicate(#ogh_2-1)	#ogh_3-2	HIDDEN_WARP_NPC,20,20
2@gl_k,256,149,0	duplicate(#ogh_2-1)	#ogh_3-3	HIDDEN_WARP_NPC,20,20
2@gl_k,212,106,0	duplicate(#ogh_2-1)	#ogh_3-4	HIDDEN_WARP_NPC,20,20
2@gl_k,243,73,0	duplicate(#ogh_2-1)	#ogh_3-5	HIDDEN_WARP_NPC,20,20
2@gl_k,229,26,0	duplicate(#ogh_2-1)	#ogh_3-6	HIDDEN_WARP_NPC,20,20
2@gl_k,181,34,0	duplicate(#ogh_2-1)	#ogh_3-7	HIDDEN_WARP_NPC,20,20

// Commander 1
2@gl_k,1,1,0	script	#ghmemorialmob05	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_2-" + .@i);
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "Где-то поблизости появилось существо с сильной злой аурой.", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 41,146, 58,44, 122,148, 131,64;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "1-й Капитан Разрушения",2473,1, instance_npcname("#ghmemorialmob05") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "Проход во вторую зону появился в направлении на 3 часа. Чтобы попасть туда, пройдите в центральный коридор.", bc_map,0xFFFF00;
	enablenpc instance_npcname("#23 Control");
	enablenpc instance_npcname("#23 Control2");
	donpcevent instance_npcname("#ghmemorialmob06") + "::OnStart";

	disablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_2-" + .@i) + "::OnStop";
	end;
}

// Commander 2
2@gl_k,1,1,0	script	#ghmemorialmob06	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_3-" + .@i);

	// Fix spawn
	.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	monster 'map_name$[1],252, 75, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],253, 76, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],247, 77, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],248, 80, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],236,100, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],240,100, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],242,100, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],244,100, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],199,111, "Сгнивший страж",2468,1, .@label$;
	monster 'map_name$[1],181,107, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],177,110, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],236, 27, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],242, 27, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],252, 26, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],172,130, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],171,127, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],173,127, "Заблудший стрелок",2469,1, .@label$;
	monster 'map_name$[1],177, 69, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],186, 66, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],189, 67, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],190, 68, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],257,157, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],246,159, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],237,158, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],206,159, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],172,120, "Падший рыцарь",2470,1, .@label$;
	monster 'map_name$[1],226, 30, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],226, 38, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],228, 29, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],226, 63, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],268,137, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],263,138, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],259,138, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],259,138, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],187,121, "Страдающий Калицбург",2471,1, .@label$;
	monster 'map_name$[1],265, 52, "Запятнанный рыцарь",2472,1, .@label$;
	monster 'map_name$[1],265, 55, "Запятнанный рыцарь",2472,1, .@label$; 
	monster 'map_name$[1],264, 79, "Запятнанный рыцарь",2472,1, .@label$; 
	monster 'map_name$[1],225, 92, "Запятнанный рыцарь",2472,1, .@label$; 
	monster 'map_name$[1],237, 92, "Запятнанный рыцарь",2472,1, .@label$; 
	monster 'map_name$[1],249, 92, "Запятнанный рыцарь",2472,1, .@label$; 
	monster 'map_name$[1],256, 93, "Запятнанный рыцарь",2472,1, .@label$; 
	monster 'map_name$[1],264,100, "Запятнанный рыцарь",2472,1, .@label$;
	monster 'map_name$[1],181,120, "Запятнанный рыцарь",2472,1, .@label$;
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "Поблизости появилось существо со злой аурой.", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob06") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 166,119, 211,45, 227,139, 245,74;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "2-й Капитан Разрушения",2474,1, instance_npcname("#ghmemorialmob06") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "Проход в третью зону появился в направлении на двенадцать часов. Чтобы попасть туда, пройдите в центральный коридор.", bc_map,0xFFFF00;
	enablenpc instance_npcname("#24 Control");
	enablenpc instance_npcname("#24 Control2");
	enablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		enablenpc instance_npcname("Труп#" + .@i);

	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_3-" + .@i) + "::OnStop";

	// Hidden mobs
	setarray .@coord[0],
		 37,265, 57,265, 77,265, 97,265, 117,265, 188,264, 208,264,
		 50,172, 70,172, 90,172, 110,172, 170,172, 210,172, 230,172,
		 88,214, 108,214, 128,214, 180,219, 200,219, 220,219, 240,219;
	.@size = getarraysize(.@coord);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		monster 'map_name$[1], .@coord[.@i], .@coord[.@i+1], "Пламя разрухи",2337,1;// HIDDEN_MOB
	end;
}

// Amdarais Room Entrance
2@gl_k,150,180,0	script	#Geron	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Geron");
	mapannounce 'map_name$[1], "???: Не подходите! Это ловушка!!! Кххх *шмяк*!!!", bc_map,0xFF7777,FW_NORMAL,15;
	enablenpc instance_npcname("Химмельмез#22");
	enablenpc instance_npcname("Вармунт#23");
	enablenpc instance_npcname("Генрих#23");
	enablenpc instance_npcname("Герхард#23");
	end;
}

2@gl_k,143,260,4	script	Труп#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, UNITTYPE_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Червь",2467,.@mobs;
	initnpctimer;
	end;
OnTimer45000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,145,236,4	duplicate(Труп#1)	Труп#2	4_M_DIEMAN,5,5
2@gl_k,141,222,2	duplicate(Труп#1)	Труп#3	4_M_DIEMAN,5,5
2@gl_k,147,203,5	duplicate(Труп#1)	Труп#4	4_M_DIEMAN,5,5
2@gl_k,167,225,4	duplicate(Труп#1)	Труп#5	4_M_DIEMAN,5,5
2@gl_k,172,233,2	duplicate(Труп#1)	Труп#6	4_M_DIEMAN,5,5
2@gl_k,176,244,3	duplicate(Труп#1)	Труп#7	4_M_DIEMAN,5,5
2@gl_k,184,248,6	duplicate(Труп#1)	Труп#8	4_M_DIEMAN,5,5
2@gl_k,193,228,0	duplicate(Труп#1)	Труп#9	4_M_DIEMAN,5,5
2@gl_k,206,250,7	duplicate(Труп#1)	Труп#10	4_M_DIEMAN,5,5
2@gl_k,130,249,1	duplicate(Труп#1)	Труп#11	4_M_DIEMAN,5,5
2@gl_k,122,236,5	duplicate(Труп#1)	Труп#12	4_M_DIEMAN,5,5
2@gl_k,130,228,7	duplicate(Труп#1)	Труп#13	4_M_DIEMAN,5,5
2@gl_k,106,226,0	duplicate(Труп#1)	Труп#14	4_M_DIEMAN,5,5
2@gl_k,104,245,0	duplicate(Труп#1)	Труп#15	4_M_DIEMAN,5,5
2@gl_k,131,187,0	duplicate(Труп#1)	Труп#16	4_M_DIEMAN,5,5
2@gl_k,121,197,0	duplicate(Труп#1)	Труп#17	4_M_DIEMAN,5,5
2@gl_k,107,194,0	duplicate(Труп#1)	Труп#18	4_M_DIEMAN,5,5
2@gl_k,92,187,0	duplicate(Труп#1)	Труп#19	4_M_DIEMAN,5,5
2@gl_k,153,214,3	duplicate(Труп#1)	Труп#20	4_M_DIEMAN,5,5
2@gl_k,155,195,4	duplicate(Труп#1)	Труп#21	4_M_DIEMAN,5,5
2@gl_k,154,188,2	duplicate(Труп#1)	Труп#22	4_M_DIEMAN,5,5
2@gl_k,143,195,5	duplicate(Труп#1)	Труп#23	4_M_DIEMAN,5,5
2@gl_k,132,214,4	duplicate(Труп#1)	Труп#24	4_M_DIEMAN,5,5
2@gl_k,125,208,2	duplicate(Труп#1)	Труп#25	4_M_DIEMAN,5,5
2@gl_k,114,210,3	duplicate(Труп#1)	Труп#26	4_M_DIEMAN,5,5
2@gl_k,137,182,6	duplicate(Труп#1)	Труп#27	4_M_DIEMAN,5,5
2@gl_k,138,246,0	duplicate(Труп#1)	Труп#28	4_M_DIEMAN,5,5
2@gl_k,132,260,7	duplicate(Труп#1)	Труп#29	4_M_DIEMAN,5,5
2@gl_k,128,251,1	duplicate(Труп#1)	Труп#30	4_M_DIEMAN,5,5
2@gl_k,179,260,3	duplicate(Труп#1)	Труп#31	4_M_DIEMAN,5,5
2@gl_k,170,261,4	duplicate(Труп#1)	Труп#32	4_M_DIEMAN,5,5
2@gl_k,177,219,2	duplicate(Труп#1)	Труп#33	4_M_DIEMAN,5,5
2@gl_k,190,214,5	duplicate(Труп#1)	Труп#34	4_M_DIEMAN,5,5
2@gl_k,201,214,4	duplicate(Труп#1)	Труп#35	4_M_DIEMAN,5,5

// Amdarais Spawn
2@gl_k,153,250,8	script	Генрих#23	4_M_HEINRICH,{
	cutin "gl_heinrich1",2;
	mes "[Генрих]";
	mes "Что ты задумала, Химмельмез!";
	close3;
}

2@gl_k,162,250,1	script	Вармунт#23	4_M_BARMUND,{
	mes "[Вармунт]";
	mes "Не могу избавиться от чувства, что мной что-то управляет...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,158,255,1	script	Герхард#23	4_LEVITATEMAN,{
	mes "[Герхард]";
	mes "Ххххх... Кхах! Бегите, бегите вместе с Комендантом.";
	close;
}

2@gl_k,158,252,1	script	Химмельмез#22	4_F_HIMEL,3,3,{
	cutin "gl_himel2",2;
	mes "[Химмельмез]";
	mes "Восхитительно! Вы забрались так далеко! Мои апплодисменты~";
	close3;

OnTouch:
	end;
OnTouch_:
	disablenpc instance_npcname("Химмельмез#22");
	enablenpc instance_npcname("Химмельмез#23");
	initnpctimer;
	'npc_himelmez$ = instance_npcname("Химмельмез#23");
	'npc_gerhalt$ = instance_npcname("Герхард#23");
	'npc_heinrich$ = instance_npcname("Генрих#23");
	'npc_varmundt$ = instance_npcname("Вармунт#23");
	end;
OnTimer3000:
	npctalk "Химмельмез : Великолепно~ Я уже думала, что вы не поспеете к концу...", 'npc_himelmez$;
	end;
OnTimer6000:
	npctalk "Герхард : Проклятье! Бегите! Я не могу больше сопротивляться...", 'npc_gerhalt$;
	end;
OnTimer9000:
	npctalk "Генрих : Герхард!", 'npc_heinrich$;
	end;
OnTimer15000:
	npctalk "Генрих : Что ты творишь с моими людьми, Химмельмез?!", 'npc_heinrich$;
	end;
OnTimer18000:
	npctalk "Химмельмез : Хохо, я успела найти осколок Сердца Имира, Генрих.", 'npc_himelmez$;
	end;
OnTimer21000:
	npctalk "Химмельмез : Если б он не мешался под ногами, всё прошло бы ещё быстрее.", 'npc_himelmez$;
	end;
OnTimer24000:
	npctalk "Герхард : Комендант... вам нужно бежать отсюда... Кх.", 'npc_gerhalt$;
	end;
OnTimer27000:
	npctalk "Химмельмез : Правда? Почему ты так думаешь?", 'npc_himelmez$;
	end;
OnTimer30000:
	npctalk "Генрих : Химмельмез! Ты уже получила, что хотела, не надо больше жертв!", 'npc_heinrich$;
	end;
OnTimer33000:
	npctalk "Генрих : Отпусти его! Хватит пострадавших!", 'npc_heinrich$;
	end;
OnTimer36000:
	npctalk "Химмельмез : Отпустить? Как насчёт... нет.", 'npc_himelmez$;
	end;
OnTimer39000:
	npctalk "Химмельмез : Ах, да, Генрих, это же твой последний солдат.", 'npc_himelmez$;
	end;
OnTimer42000:
	npctalk "Химмельмез : К тому же он затруднил моё продвижение.", 'npc_himelmez$;
	end;
OnTimer45000:
	npctalk "Химмельмез : Он идеально подходит для моего нового творения... Амдараиса.", 'npc_himelmez$;
	end;
OnTimer48000:
	npctalk "Генрих : Я никогда не прощу тебя.", 'npc_heinrich$;
	end;
OnTimer51000:
	npctalk "Химмельмез : Ууух~ Как страшно.", 'npc_himelmez$;
	end;
OnTimer54000:
	npctalk "Химмельмез : Но~ мне было приятно поговорить с тобой. Может быть, нам удастся встретиться в следующий раз.", 'npc_himelmez$;
	end;
OnTimer57000:
	npctalk "Химмельмез : До скорого.", 'npc_himelmez$;
	end;
OnTimer60000:
	specialeffect EF_BARRIER, AREA, 'npc_gerhalt$;
	end;
OnTimer63000:
	disablenpc 'npc_himelmez$;
	end;
OnTimer65000:
	npctalk "Герхард: Ты можешь осквернить моё тело, но не душу, Химмельмез!", 'npc_gerhalt$;
	end;
OnTimer66000:
	specialeffect EF_CHAINCOMBO, AREA, 'npc_gerhalt$;
	end;
OnTimer67000:
	npctalk "Генрих : Тебе не уйти, Химмельмез!!", 'npc_heinrich$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	end;
OnTimer70000:
	specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	disablenpc 'npc_heinrich$;
	npctalk "Вармунт : У нас нет выбора. Придётся сразиться с Амдараисом!", 'npc_varmundt$;
	end;
OnTimer73000:
	mapannounce 'map_name$[1], "Тело Герхарда начинает меняться.", bc_map,0xFFFFFF;
	end;
OnTimer76000:
	specialeffect EF_LORD, AREA, 'npc_gerhalt$;
	disablenpc 'npc_varmundt$;
	mapannounce 'map_name$[1], "Вармунт : Я помогу. Делай, что скажет моя иллюзия.", bc_map,0xFFFF00;
	end;
OnTimer80000:
	disablenpc 'npc_gerhalt$;
	donpcevent instance_npcname("#ghmemorialmob07") + "::OnStart";
	stopnpctimer;
	'npc_himelmez$ = 'npc_gerhalt$ = 'npc_heinrich$ = 'npc_varmundt$ = "";
	end;
}

2@gl_k,158,252,4	script	Химмельмез#23	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[Химмельмез]";
	mes "Восхитительно! Вы забрались так далеко! Мои апплодисменты~";
	close3;
}

2@gl_k,1,1,0	script	#ghmemorialmob07	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob07");
	monster 'map_name$[1],158,255,"Амдараис",2476,1,instance_npcname("#ghmemorialmob07") + "::OnMyMobDead";// MG_AMDARAIS
	'boss_id = $@mobid[0];
	unittalk 'boss_id, "Беги... Беги... от меня...";
	initnpctimer;
	end;
OnTimer5000:
	unittalk 'boss_id, "Я не хочу... Я не хочу убивать... Ааааа~";
	end;
OnTimer10000:
	unittalk 'boss_id, "Убейте меня~ Пожалуйста~";
	end;
OnTimer16000:
	unittalk 'boss_id, "Ээаэээааа... эээ... убей... убей...";
	end;
OnTimer22000:
	unittalk 'boss_id, "Разнеси... Смерть!...";
	end;
OnTimer55000:
	donpcevent instance_npcname("#ghmemorialmob08") + "::OnStart";// Varmundt buffs and additionnal monsters
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob07") + "::OnMyMobDead") < 1) {
		stopnpctimer;
		enablenpc instance_npcname("Хугин#21");
		enablenpc instance_npcname("#Secret Room Exit");

		stopnpctimer;
		disablenpc instance_npcname("#ghmemorialmob07");
		// note: monsters from #ghmemorialmob08 still alive on Amdarais's dead
	}
	end;
}

// Varmundt Buffs
2@gl_k,1,1,0	script	#ghmemorialmob08	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob08");
	initnpctimer;
	end;
OnTimer5000:
	if (unitexists('boss_id) == 0) {
		disablenpc instance_npcname("#ghmemorialmob08");
		stopnpctimer;
		end;
	}
	getunitdata 'boss_id, .@data;
	.@percent_hp = (.@data[UMOB_HP] * 100) / .@data[UMOB_MAXHP];
	mapannounce 'map_name$[1], "У Амдараиса осталось " + .@percent_hp + "% здоровья!", bc_map,0x70DBDB;

	// event type every 10%
	switch( .@percent_hp / 10 ) {
	case 10:
		break;
	case 9:
		donpcevent instance_npcname("Призрак Вармунта#Buff2") + "::OnEvent";
		break;
	case 8:
		donpcevent instance_npcname("Призрак Вармунта#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Страдающий Калицбург",2471,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 7:
		donpcevent instance_npcname("Призрак Вармунта#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Рыцарь Бездны",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 6:
		donpcevent instance_npcname("Призрак Вармунта#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Запятнанный рыцарь",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 5:
		donpcevent instance_npcname("Призрак Вармунта#Buff2") + "::OnEvent";
		break;
	case 4:
		donpcevent instance_npcname("Призрак Вармунта#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Заблудший стрелок",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 3:
		donpcevent instance_npcname("Призрак Вармунта#Buff1") + "::OnEvent";
		donpcevent instance_npcname("Призрак Вармунта#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Запятнанный рыцарь",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 2:
		donpcevent instance_npcname("Призрак Вармунта#Buff1") + "::OnEvent";
		donpcevent instance_npcname("Призрак Вармунта#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Рыцарь Бездны",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 1:
	case 0:
		donpcevent instance_npcname("Призрак Вармунта#Buff1") + "::OnEvent";
		donpcevent instance_npcname("Призрак Вармунта#Buff4") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"Заблудший стрелок",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	}
	end;
OnTimer30000:
	killmonster 'map_name$[1], instance_npcname("#ghmemorialmob08") + "::OnMobDead";
	initnpctimer;
	end;
OnMobDead:
	end;
}

2@gl_k,150,247,5	script	Призрак Вармунта#Buff1	4_M_BARMUND,2,2,{
	end;
OnTouch:
	specialeffect2 EF_ENHANCE;
	// buff
	// .@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	// if (.@num == 1)
	// else if (.@num == 2)
	// else if (.@num == 3)
	// else
	end;
OnEvent:
	initnpctimer;
	enablenpc instance_npcname( strnpcinfo(0) );
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		npctalk "Призрак Вармунта : Амдараис обладает собственной магией! Держитесь ближе ко мне, я вас прикрою!";
	else if (.@num == 2)
		npctalk "Призрак Вармунта : Время для удара! Встаньте около меня и атакуйте!";
	else if (.@num == 3)
		npctalk "Призрак Вармунта : Если не хотите, чтобы вас атаковали зомби Амдараиса, подойдите ко мне и зачерпните силы!";
	else
		npctalk "Призрак Вармунта : Если не хотите, чтобы вас атаковали зомби Амдараиса, подойдите ко мне и зачерпните силы! ";
	end;
OnTimer10000:
	stopnpctimer;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
2@gl_k,165,247,3	duplicate(Призрак Вармунта#Buff1)	Призрак Вармунта#Buff2	4_M_BARMUND,2,2
2@gl_k,150,232,8	duplicate(Призрак Вармунта#Buff1)	Призрак Вармунта#Buff3	4_M_BARMUND,2,2
2@gl_k,165,232,2	duplicate(Призрак Вармунта#Buff1)	Призрак Вармунта#Buff4	4_M_BARMUND,2,2


2@gl_k,158,241,1	script	Хугин#21	4_M_SAGE_C,{
	if (checkquest(12319,HUNTING) == 2) {// note: complete hunting 'Corrupted Soul Hunt' prerequis
		mes "[Хугин]";
		mes "Хорошая работа. Прежде всего возьмите награду за Амдараиса.";
		erasequest 12319;
		setquest 12322;// Space Distortion
		if (isbegin_quest(12321) == 0) {
			setquest 12321;// Time Conqueror
			completequest 12321;
			getitem 6607,5;// Temporal_Crystal
			getitem 6608,5;// Coagulated_Spell
			getexp 350000,350000;
		}
		else {
			getitem 6607,1;// Temporal_Crystal
			getitem 6608,1;// Coagulated_Spell
		}
		next;
		mes "[Хугин]";
		mes "Я остановил время Вармунта.";
		mes "А ваше скоро подвергнется искривлению.";
		specialeffect2 EF_BLIND;
		soundeffect "_blind.wav",0;
		next;
		mes "[Хугин]";
		mes "Возможно, путешественники вновь попытаются остановить трагедию Гластхейма.";
		next;
		mes "[Хугин]";
		mes "Однако некоторые вещи не изменить... Например, поступки и помыслы людей живших давно в прошлом...";
		next;
		mes "[Хугин]";
		mes "А теперь позвольте я сотру вам память. Если мы встретимся снова, это будет как в первый раз.";
		specialeffect2 EF_FREEZE;
		close2;
		warp 'map_name$[1],158,244;
		end;
	}
	mes "[Хугин]";
	mes "Временной разлом почти полностью истощён. Нужно выбираться, пойдёмте.";
	next;
	select("Вы же только что были вот тут...");
	mes "[Хугин]";
	mes "Что вы сказали?";
	mes "Ах, не важно. Разлом затягивается, нужно поспешить.";
	next;
	if (select("Я хочу оглядеться:На выход") == 1) {
		mes "[Хугин]";
		mes "Правда? Что ж, только не задерживайтесь.";
		close;
	}
	close2;
	warp "glast_01",204,270;
	end;
}

// Treasure Room
//============================================================
1@gl_k,165,136,3	script	Трещина в пространстве#2	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "Странного вида трещина и всё.";
		close;
	}
	specialeffect EF_SPELLBREAKER;

	.@random = rand(1,4);
	switch( atoi(strnpcinfo(2)) ) {
	case 2:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 719,1,"this",165,138;
		makeitem 6608,1,"this",165,138;
		break;
	case 3:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 720,1,"this",159,138;
		if (rand(1,4) == 4)
			makeitem2 15066,1,"this",159,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",159,138;
		makeitem 7229,1,"this",159,138;
		break;
	case 4:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 721,1,"this",153,138;
		if (rand(1,4) == 4)
			makeitem2 13086,1,"this",153,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",153,138;
		makeitem 7230,1,"this",153,138;
		break;
	case 5:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 722,1,"this",147,138;
		if (rand(1,4) == 4)
			makeitem2 2949,1,"this",147,138,0,0,0,0,0,0,0;
		makeitem 6612,1,"this",147,138;
		makeitem 6613,1,"this",147,138;
		makeitem 6608,1,"this",147,138;
		break;
	case 6:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 725,1,"this",141,138;
		makeitem 7228,1,"this",141,138;
		if (rand(1,4) == 4)
			makeitem2 13440,1,"this",141,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",141,138;
		break;
	case 7:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 726,1,"this",135,138;
		if (rand(1,4) == 4)
			makeitem2 2022,1,"this",135,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",135,138;
		break;
	case 8:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 727,1,"this",129,138;
		if (rand(1,4) == 4)
			makeitem2 21007,1,"this",129,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",129,138;
		break;
	}
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,159,136,3	duplicate(Трещина в пространстве#2)	Трещина в пространстве#3	CLEAR_NPC
1@gl_k,153,136,3	duplicate(Трещина в пространстве#2)	Трещина в пространстве#4	CLEAR_NPC
1@gl_k,147,136,3	duplicate(Трещина в пространстве#2)	Трещина в пространстве#5	CLEAR_NPC
1@gl_k,141,136,3	duplicate(Трещина в пространстве#2)	Трещина в пространстве#6	CLEAR_NPC
1@gl_k,135,136,3	duplicate(Трещина в пространстве#2)	Трещина в пространстве#7	CLEAR_NPC
1@gl_k,129,136,3	duplicate(Трещина в пространстве#2)	Трещина в пространстве#8	CLEAR_NPC

1@gl_k,269,267,3	script	Трещина в пространстве#1	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "Странного вида трещина и всё.";
		close;
	}
	warp 'map_name$[0],149,198;
	end;

OnInstanceInit:
	'map_name$[0] = instance_mapname("1@gl_k");
	'map_name$[1] = instance_mapname("2@gl_k");

	// Entrance
	disablenpc instance_npcname("Химмельмез#1");
	disablenpc instance_npcname("Вармунт#1");
	disablenpc instance_npcname("Генрих#1");
	disablenpc instance_npcname("Генрих#2");
	disablenpc instance_npcname("Генрих#3");

	// Rescue 1
	disablenpc instance_npcname("#ghmemorialmob01");
	disablenpc instance_npcname("Прислужник Домун#1");

	// Rescue 2
	disablenpc instance_npcname("#ghmemorialmob02");
	disablenpc instance_npcname("Холлгренн#1");
	for ( .@i = 1; .@i <= 26; .@i++ )
		disablenpc instance_npcname("Мертвец#" + .@i);

	// Sector 3
	disablenpc instance_npcname("#ghmemorialmob03");

	// Root of Corruption
	disablenpc instance_npcname("Химмельмез#2");
	disablenpc instance_npcname("Вармунт#2");
	disablenpc instance_npcname("Генрих#4");
	disablenpc instance_npcname("#Mimelon");
	disablenpc instance_npcname("#ghmemorialmob04");

	// Entrance 2nd map
	disablenpc instance_npcname("Вармунт#21");
	disablenpc instance_npcname("Генрих#21");
	disablenpc instance_npcname("#Servanton");
	hideonnpc instance_npcname("#Servanton_effect");

	// Commanders
	disablenpc instance_npcname("#ghmemorialmob05");
	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ ) {
		disablenpc instance_npcname("#ogh_2-" + .@i);
		disablenpc instance_npcname("#ogh_3-" + .@i);
	}

	// Amdarais Room Entrance
	disablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		disablenpc instance_npcname("Труп#" + .@i);

	// Amdarais Spawn
	disablenpc instance_npcname("Герхард#23");
	disablenpc instance_npcname("Химмельмез#22");
	disablenpc instance_npcname("Химмельмез#23");
	disablenpc instance_npcname("Вармунт#23");
	disablenpc instance_npcname("Генрих#23");
	disablenpc instance_npcname("#ghmemorialmob07");

	// Varmundt Buffs
	disablenpc instance_npcname("#ghmemorialmob08");
	disablenpc instance_npcname("Призрак Вармунта#Buff1");
	disablenpc instance_npcname("Призрак Вармунта#Buff2");
	disablenpc instance_npcname("Призрак Вармунта#Buff3");
	disablenpc instance_npcname("Призрак Вармунта#Buff4");
	disablenpc instance_npcname("Хугин#21");

	// Warps
	disablenpc instance_npcname("#2Control");
	disablenpc instance_npcname("#2Control2");
	disablenpc instance_npcname("#3Control");
	disablenpc instance_npcname("#3Control2");
	disablenpc instance_npcname("#4Control");
	disablenpc instance_npcname("#4Control2");
	disablenpc instance_npcname("#2F Entrance");
	disablenpc instance_npcname("#1 Control");
	disablenpc instance_npcname("#22 Control");
	disablenpc instance_npcname("#22 Control2");
	disablenpc instance_npcname("#23 Control");
	disablenpc instance_npcname("#23 Control2");
	disablenpc instance_npcname("#24 Control");
	disablenpc instance_npcname("#24 Control2");
	disablenpc instance_npcname("#Secret Room Exit");
	end;
}
