//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 
//= 06.06.2020 Перевод Xsoid
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Мы идём к Эйнброх.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Мы скоро прибудем в Эйнброх.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Добро пожаловать в Эйнброх. Путешествуйте безопасно.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","В настоящее время мы находимся в Эйнброх. Дирижабль снимается с якоря в ближайшее время.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Дирижабль навзлёте. Наш следующий пункт назначения Лайтхайзен.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Мы направляемся в Лайтхайзен.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Мы прибудем в Лайтхайзен в ближайшее время.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Добро пожаловать в Лайтхайзен. Путешествуйте безопасно.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","В настоящее время мы находимся в Лайтхайзен. Дирижабль снимается с якоря в ближайшее время.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Дирижабль покидает землю. Наш следующий пункт назначения Эйнброх.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Мы направляемся в Эйнброх.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Мы прибудем в Эйнброх в ближайшее время.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Добро пожаловать в Эйнброх. Путешествуйте безопасно.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","В настоящее время мы находимся в Эйнброх. Дирижабль снимается с якоря в ближайшее время.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Дирижабль навзлёте. Наш следующий пункт назначения Юно.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Мы направляемся в Юно.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Мы прибудем в Юно в ближайшее время.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Добро пожаловать в Юно. Путешествуйте безопасно.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","В настоящее время мы находимся в Юно. Дирижабль отправляется в ближайшее время.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Дирижабль покидает землю. Наш следующий пункт назначения Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Мы направляемся в Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Мы прибудем в Hugel в ближайшее время.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Добро пожаловать в Hugel. Путешествуйте в безопасности.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","В настоящее время мы находимся в Hugel. Дирижабль скоро отправляется.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Дирижабль покидает землю. Наш следующий пункт назначения Юно.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Мы направляемся в Юно.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Мы прибудем в Юно в ближайшее время.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Добро пожаловать в Юно. Путешествуйте в безопасности.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","В настоящее время мы находимся в Юно. Дирижабль скоро отправляется.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Дирижабль покидает землю. Наш следующий пункт назначения Эйнброх.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Выход#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Выход#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Выход#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Выход#airplane2b	857

airplane,100,69,3	script	Экипаж дирижабля#ein-1	852,{
	mes "[Экипаж дирижабля]";
	mes "Когда мы приземляемся в пункте назначения и вы хотите выйти, используйте для этого лестницу перед вами. Благодарим вас за использование наших авиалиний.";
	close;
}

airplane,64,94,1	script	Малыш Умбала#ein_p	787,{
	emotion ET_PROFUSELY_SWEAT;
	mes "[Киндер]";
	if (event_umbala >= 3) {
		mes "Воу! Только посмотри на это! Мы летим! Мы на седьмом небе!";
	}
	else {
		mes "Макумалагу!";
		mes "Сампа джуджимбо каку на джеди Соло.";
		mes "Бвахахахахахахаха!";
	}
	close;
}

airplane,66,93,3	script	Леди из Умбалы#ein_p	783,{
	emotion ET_THINK;
	mes "[Леди]";
	if (event_umbala >= 3) {
		mes "Ш-ш-ш...";
		mes "Тише, малыш.";
		mes "Веди себя спокойней на людях.";
	}
	else {
		mes "Чукту!";
		mes "Сакрауп матии!";
		mes "Шака гурфталфи";
		mes "хунтики манджо!";
	}
	close;
}

airplane,71,91,7	script	Человек из Умбалы#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Хрмлим]";
		mes "Эй ты~";
		mes "Не отворачивайся. Судя по твоему лицу ты меня понимаешь.";
		next;
		emotion ET_HNG;
		mes "[Хрмлим]";
		mes "Я помогаю членам экипажа преодолеть боязнь высоты.";
		next;
		mes "[Хрмлим]";
		mes "Но...";
		mes "Некоторым так и не удаётся преодолеть свои страхи и они... гм... Попадают ^333333в Ниффльхейм^000000.";
	}
	else {
		mes "[Хрмлим]";
		mes "Баджо га";
		mes "нукта Дирижабль.";
		next;
		mes "[Хрмлим]";
		mes "...";
		mes "......";
		next;
		emotion ET_HNG;
		mes "[Хрмлим]";
		mes "Шабала мув баджама";
		mes "Дирижабль кулаха гугуна";
		mes "салу. Дама, куку на ну";
		mes "укута. Ум, фашуку на рет!";
	}
	close;
}

airplane,250,58,2	script	Персонал дирижабля#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Персонал дирижабля]";
		mes "Добро пожаловать на дирижабль.";
		mes "Как я могу тебе помочь?";
		next;
		select("У вас есть пасажир по имени Thierry?");
		mes "[Персонал дирижабля]";
		mes "Извините, не думаю что у нас есть пассажир с таким именем.";
		close;
	}
	// Hugel quest end
	mes "[Персонал дирижабля]";
	mes "Добро пожаловать на дирижабль.";
	mes "Как я могу тебе помочь?";
	next;
	switch (select("Я хочу полететь:Мне нужна каюта капитана:Как я могу провести время:Ничего")) {
	case 1:
		mes "[Персонал дирижабля]";
		mes "Когда ты увидишь объявление о посадке, воспользуйся выходом в северной или южной части дирижабля.";
		next;
		mes "[Персонал дирижабля]";
		mes "Если ты пропустишь свою остановку, не переживай, дирижабль постоянно находится на маршруте и рано или поздно вернется к необходимой точке высадки.";
		close;
	case 2:
		mes "[Персонал дирижабля]";
		mes "Каюта капитана находится во фронтальной части дирижабля. Там ты сможешь найти капитана и пилота.";
		close;
	case 3:
		mes "[Персонал дирижабля]";
		mes "На дирижабле есть небольшое казино, в которое приглашаются все пасажиры. Там ты можешь скоротать время за мини-играми и испытать свою удачу.";
		close;
	case 4:
		mes "[Персонал дирижабля]";
		mes "Надеюсь полёт пройдёт приятно. Доброй дороги.";
		close;
	}
}

airplane,80,71,2	script	Зерта#01airplane	834,{
	mes "[Зерта]";
	mes "Привет, авантюрист.";
	mes "В настоящее время я нахожусь в крестовом походе. Помолись со мной за Рун-Мидгард.";
	close;
}

airplane,65,63,4	script	Маэлин#01airplane	714,{
	mes "[Маэлин]";
	mes "Этот дирижабль летит в Лютию? Я лечу так давно, но не слышала ни одного объявления о Лютии.";
	close;
}

airplane,72,34,6	script	Энос#01airplane	702,{
	mes "[Энос]";
	mes "Ох. Отсюда небо выглядит совершенно по-другому.";
	close;
}

airplane,221,158,2	script	Пилот#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Пилот]";
		mes "Как же я хочу злоупотребить свежим холодным пивком.";
		mes "Цель моей жизни пить!";
		mes "Только выпивка придаёт мне энергию!";
		next;
		mes "[Пилот]";
		mes "Но! Пить на работе непозволительно.";
		mes "Но! Из-за этого мне только больше хочется выпить!";
		emotion ET_CRY;
		next;
		select("Знаете ли вы посажира по имени Thierry?");
		mes "[Пилот]";
		mes "Эта униформа действительно крутая, но слишком толстая и неудобная, чтобы носить её на дирижабле.";
		next;
		mes "[Пилот]";
		mes "...";
		mes "......";
		mes "На самом деле сюда никто никогда не заходит.";
		mes "Да и капитан северный олень.";
		mes "Я вполне мог бы раздеться до трусов.";
		next;
		emotion ET_HUK;
		mes "[Пилот]";
		mes "Вах! Кто тут?";
		next;
		mes "- ...Он ничего не слышал. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Пилот]";
		mes "Это было ооочень давно. Я наслаждался прекрасным алкогольным напитком. Но работа требует ясного ума.";
		next;
		mes "[Пилот]";
		mes "Всегда пейте ответственно!";
		mes "Тем не менее я не могу вспомнить, когда у меня был реальный отпуск или хоты бы выходной.";
		mes "Дааа, немного выпивки, азартных игр, немного телевизора и всё, пришёл бы в полный порядок.";
		emotion ET_CRY;
		close;
	case 2:
		mes "[Пилот]";
		mes "Сегодня отличная погода и ясные небеса. Из некоторых окон и с палубы открывается прекрасный вид.";
		close;
	case 3:
		mes "[Пилот]";
		mes "Вы знаете, наш капитан отличный парень. На самом деле он и его брат хорошо известны в авиации. Кто бы мог подумать, что из оленей получаются такие хорошие капитаны?";
		next;
		mes "[Пилот]";
		mes "Между нами говоря, Санта явно что-то знал когда брал на работу оленей и эльфов. Он либо гений, либо чертовски удачливый парень.";
		close;
	default:
		mes "[Пилот]";
		mes "Вы знаете? Всё это плавание в воздухе... То есть пилотирование корабля. Всё это довольно новое дело. Я получил эту работу в ужасной спешке.";
		next;
		emotion ET_HUK;
		mes "[Пилот]";
		mes "Но мои наниматели были абсолютно серьёзны! И нас хорошо тренировали!";
		close;
	}
}

airplane,50,66,5	script	Торговец яблоками#airplane	86,{
	mes "[Фруктоз]";
	mes "Добро пожаловать в магазин фруктов. Здесь вы можете купить яблок из которых сможете сделать яблочный сок.";
	next;
	switch (select("Купить яблоки.:Сделать яблочный сок.:Зачем ты здесь?:Уйти.")) {
	case 1:
		mes "[Фруктоз]";
		mes "Сколько яблок тебе нужно? Каждое яблоко будет стоит 15 зен. К сожалению за один раз я не могу продать более 500 яблок.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Фруктоз]";
				mes "Спасибо что выбрали наш магазин. Удачи! Приходите в любое время мы работаем без перерывов и выходных.";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Фруктоз]";
				mes "Нет-нет. Я не могу продать более 500 яблок за раз. Укажи другое количество, друг.";
				next;
			}
			else {
				mes "[Фруктоз]";
				mes "И так ^FF0000" + .@input + "^000000 яблок";
				mes "будут стоить ^FF0000" + .@pay + " Zeny^000000 зен.";
				mes "Оплачиваешь?";
				next;
				if (select("Да:Нет") == 2) {
					mes "[Фруктоз]";
					mes "Спасибо что выбрал наш магазин. Удачи! Приходите в любое время мы работаем без перерывов и выходных.";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Фруктоз]";
			mes "Кажется у тебя не хватает денег. Проверь свои карманы. Если тебе не достаточно денег может просто купишь не так много?";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Фруктоз]";
			mes "Ты не можешь унести так много яблок. Тебе нужно избавится от каких то вещей или купить меньше яблок.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Фруктоз]";
			mes "Спасибо что выбрали наш магазин. Я надеюсь наши яблоки придутся вам по вкусу!";
			close;
		}
	case 2:
		mes "[Фруктоз]";
		mes "Хорошо. Для изготовления одного яблочного сока потребуется ^FF00003 1 Яблоко и 1 Пустая бутылка^000000. У тебя всё есть?";
		next;
		switch (select("Да:Нет")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Фруктоз]";
				mes "Нет. Всё таки чего-то у тебя не хватает. Проверь ещё раз. Для одного яблочного сока нужна одно яблоко и одна пустая бутылка.";
				close;
			}
			else {
				mes "[Фруктоз]";
				mes "Спасибо, пожалуйста, подожди минутку.";
				next;
				mes "^3355FF*Измельчить натереть*";
				mes "*Измельчить натереть*";
				mes "*Дзынь...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Фруктоз]";
				mes "Ну вот и всё~";
				mes "Я надеюсь вам понравится и вы ещё не раз обратитесь ко мне за соком.";
				close;
			}
		case 2:
			mes "[Фруктоз]";
			mes "Спасибо что выбрал наш магазин. Удачи! Приходите в любое время мы работаем без перерывов и выходных.";
			close;
		}
	case 3:
		mes "[Фруктоз]";
		mes "Раньше я был странствующим бродягой, когда однажды я вздремнул, что-то ударило меня по голове и разбудило меня от сна.";
		next;
		mes "[Фруктоз]";
		mes "Оказывается, я спал под яблоней, яблоко упало и ударило меня по голове.";
		mes "Я умирал с голоду и собирался съесть это яблоко...";
		next;
		mes "[Фруктоз]";
		mes "Но вдруг Каин, мой старый друг шахтеров, попросил меня помочь ему с дирижаблем. Я согласился, и тогда здесь я нашел людей, играющих в кости.";
		next;
		mes "[Фруктоз]";
		mes "Мне было скучно и любопытно, и в итоге я поставил это единственное яблоко в на кон в игре. И по какой-то причине у меня началась невероятная полоса везения. Одно яблоко стало двумя... ";
		next;
		mes "[Фруктоз]";
		mes "Два стало четырьмя, и прежде чем я это осознал, я захватил рынок Яблок!";
		mes "Я выиграл так много яблок, что только что начал свой собственный бизнес здесь, на дирижабле. Странно, да?";
		next;
		mes "[Фруктоз]";
		mes "Так что яблоки полезны. Они, конечно, были очень добры ко мне. Хахахахах~!";
		close;
	case 4:
		mes "[Фруктоз]";
		mes "Спасибо за посещение моего магазина.";
		mes "Прощай~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Мы направляемся в Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Мы скоро прибудем в Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Добро пожаловать в Izlude. Путешествуйте в безопасности.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Мы находимся в Izlude. Дирижабль скоро отправляется в путь.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Дирижабль на взлёте. Наш следующий пункт назначения Юно.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Мы направляемся в Юно.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Мы скоро прибудем в Юно.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Добро пожаловать в Юно. Путешествуйте в безопасности.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Мы находимся в Юно. Дирижабль скоро отправляется в путь.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Дирижабль покидает землю. Наш следующий пункт назначения Рашель.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Мы направляемся в Рашель.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Мы скоро прибудем в Рашель.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Добро пожаловать в Рашель. Путешествуйте в безопасности.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Мы находимся в Рашель. Дирижабль скоро отправляется в путь.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Дирижабль покидает землю. Наш следующий пункт назначения Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Выход#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Выход#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Выход#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Выход#airplane_012b	857

airplane_01,250,58,2	script	Персонал дирижабля#airplane01	67,{
	mes "[Персонал дирижабля]";
	mes "Добро пожаловать";
	mes "на Дирижабль.";
	mes "Чем я могу вам помочь?";
	next;
	switch (select("Инструктаж к полёту:Кабина Капитана:Развлечения:Отмена")) {
	case 1:
		mes "[Персонал дирижабля]";
		mes "Когда вы увидите сообщение о том, что мы прибыли в пункт назначения, пожалуйста, воспользуйтесь одним из выходов, расположенных на северном и южном концах Дирижабля.";
		next;
		mes "[Персонал дирижабля]";
		mes "Если вы случайно пропустите свою остановку, не волнуйтесь. Дирижабль постоянно курсирует по маршруту, и у вас будет возможность выйти на сввоей станции.";
		close;
	case 2:
		mes "[Персонал дирижабля]";
		mes "Каюта капитана расположена в передней части дирижабля. Там вы можете познакомиться с капитаном и пилотом Дирижабля.";
		close;
	case 3:
		mes "[Персонал дирижабля]";
		mes "Наш борт предлагает различные игры для того что бы скоротать время полёта. Мы приглашаем вас испытать свою удачу и мастерство в игральных залах Дирижабля ~";
		close;
	case 4:
		mes "[Персонал дирижабля]";
		mes "Я надеюсь, что вы совершите свой полет на борту наших дирижаблей. Спасибо вам и хорошего дня.";
		close;
	}
}

airplane_01,50,66,5	script	Торговец яблоками#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Подожди минутку!! -";
		mes "- Сейчас у тебя слишком много -";
		mes "- предметов с собой. -";
		mes "- Приходите когда будет куда -";
		mes "- складывать вещи. -";
		close;
	}
	mes "[Мельтц]";
	mes "Добро пожаловать в магазин Метз, где вы можете приобрести яблоки или приготовить яблочный сок.";
	next;
	switch (select("Купить яблоки.:Яблочный сок.:Отмена.")) {
	case 1:
		mes "[Мельтц]";
		mes "Пожалуйста, укажите сколько яблок вы хотите купить. Каждое яблоко стоит 15 зен, вы можете купить максимум 500 штук за раз. Введите '0', чтобы отменить свой заказ.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Мельтц]";
				mes "Спасибо, что заглянули в мой магазин. Прощайте! Заходите в любое время, когда захочется перекусить яблок~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Мельтц]";
				mes "Вы ввели больше 500. Пожалуйста, введите количество яблок, которые вы хотите приобрести еще раз.";
				next;
			}
			else {
				mes "[Мельтц]";
				mes "В общей сложности ^FF0000" + .@input + "^000000 яблок обойдется вам в ^FF0000" + .@pay + "^000000 зен. Готовы расплотиться?";
				next;
				if (select("Да:Нет") == 2) {
					mes "[Мельтц]";
					mes "Спасибо, что заглянули в мой магазин. Прощайте! Заходите в любое время, когда захочется перекусить яблоками~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Мельтц]";
			mes "Мне жаль, но у вас недостаточно денег.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Мельтц]";
			mes "Хм, не помещаются, у вас хватает места, чтобы унести столько яблок. Возможно, вы захотите освободить место в своем инвентаре или взять меньше яблок.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Мельтц]";
			mes "Спасибо, что заглянули в мой магазин. Я надеюсь, вам понравится вкус этих яблок ~!";
			close;
		}
	case 2:
		mes "[Мельтц]";
		mes "Окей, что бы приготовить для вас 1 сок [Apple Juice] мне потребуется: ^FF0000 3 Яблока [Apples] и 1 пустая бутылка [Empty Bottle]^000000.";
		mes "Устраиввает?";
		next;
		switch (select("Да:Нет")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Мельтц]";
				mes "Мне очень жаль, но у вас недостаточно материалов. Напоминаю, для изготавления яблочного сока мне нужно от вас 3 Яблока [Apples] и 1 Пустая бутылка [Empty Bottle].";
				close;
			}
			else {
				mes "[Мельтц]";
				mes "Спасибо, пожалуйста, подождите.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Вжих* *Вжух*";
				mes "*Дзынь...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Мельтц]";
				mes "Вот так~";
				mes "Вот, пожалуйста, приходите ещё.";
				close;
			}
		case 2:
			mes "[Мельтц]";
			mes "Спасибо, что заглянули в мой магазин. До скорой встречи! Заходите в любое время, когда вам захочется сладких яблочек~";
			close;
		}
	case 3:
		mes "[Мельтц]";
		mes "Спасибо, что заглянули в мой магазин. До скорой встречи! Заходите в любое время, когда вам захочется сладких яблочек~";
		close;
	}
}

airplane_01,221,158,2	script	Пилот#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Пилот]";
		mes "131 градус восточной долготы.";
		mes "37 градусов северной широты.";
		mes "Мы на правильном курсе, капитан.";
		close;
	case 2:
		mes "[Пилот]";
		mes "День пасмурный. Трудно ориентироваться, когда небо в тучах. Думаю, нам нужно усовершенствовать радар.";
		close;
	case 3:
		mes "[Пилот]";
		mes "Капитан - хороший человек, и я не могу представить себе более подходящего человека, который мог бы командовать этим кораблем. Тем не менее, он довольно жёсткий, настоящий погонщик рабов.";
		next;
		mes "[^ff0000Тарлок^000000]";
		mes "^ff0000Эй...!^000000";
		mes "^ff0000Меньше болтовни^000000";
		mes "^ff0000и больше работы!^000000";
		next;
		mes "[Пилот]";
		mes "Р-немедленно, сэр!";
		mes "(Понимаете, что я имею в виду?)";
		close;
	default:
		mes "[Пилот]";
		mes "Эта форма действительно элегантная, но она слишком дорогая, чтобы носить ее на Дирижабле.";
		next;
		mes "[Пилот]";
		mes "...";
		mes "......";
		mes "В действительности, сюда никто никогда не заходит. А капитан вообще - северный олень. Всем будет всё равно даже разденься я до трусов.";
		next;
		emotion ET_HUK;
		mes "[Пилот]";
		mes "О...! Привет!";
		mes "Э-эм-наслаждаешься полётом?!";
		close;
	}
}

airplane_01,83,61,2	script	Диана#01airplane_01	72,2,2,{
	mes "[Диана]";
	mes "Это так странно!";
	mes "Я пошла попривветствовать капитана, но всё что я нашла, это странного северного оленя. Ох!";
	mes "Как ты думаешь...";
	close;

OnTouch_:
	emotion ET_CRY;
	end;
}

airplane_01,69,63,2	script	Мендел#01airplane_01	55,{
	mes "[Мендел]";
	mes "Как я и ожидал, питание в полете в лучшем случае трехзвездочного качества.";
	mes "*Хм-м-м* Мне действительно следовало взять с собой своего шеф-повара, чтобы я мог насладиться настоящей едой.";
	close;
}

airplane_01,71,31,2	script	Фехтовальщик Симидзу#air_01	106,{
	mes "[Фехтовальщик Симидзу]";
	mes "Наконец-то, после";
	mes "пяти лет ожидания...";
	mes "Я могу отомстить!";
	next;
	mes "[Фехтовальщик Симидзу]";
	mes "Я просто...";
	mes "Нужно быть внимательней, что бы не пропустить свою остановку. Но скоро, совсем скоро, месть свершиться!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Нилс#ein	49,1,1,{
	mes "[Нилс]";
	mes "Добро пожаловать в";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "Хотели бы вы поиграть в игру быстрого набора текста?";
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000:Информация:Посмотреть лучшие результаты:Отмена")) {
	case 1:
		mes "[Нилс]";
		mes "Okay, we have";
		mes "a new challenger!";
		mes "Enter the following";
		mes "text as quickly as you";
		mes "can without making any";
		mes "mistakes! Let's start~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Нилс]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Нилс]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Нилс]";
			mes "Your record is ^ff0000" + .@total_time + " seconds^000000 and";
			mes "the total letters are " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Нилс]";
				mes "Hmmm, this record isn't";
				mes "humanly possible unless you";
				mes "copy and paste the whole";
				mes "sentence. Please play fairly";
				mes "next time.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Нилс]";
				mes "The previous top record was";
				mes "made by ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "with the total ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "However, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "you made the new top record";
				mes "this time. Congratulations!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Нилс]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[Нилс]";
			mes "Oooh...";
			mes "I'm sorry, but";
			mes "you entered the";
			mes "text incorrectly...";
			close;
		}
	case 2:
		mes "[Нилс]";
		mes "The ^ff0000RO Typing Challenge^000000";
		mes "is a game where you enter";
		mes "the given text as quickly as you";
		mes "can. The name of the top player";
		mes "is recorded for posterity. If you";
		mes "want fame, here's your chance!";
		next;
		mes "[Нилс]";
		mes "I'd just like to let";
		mes "you know that you type";
		mes "all the text that you see";
		mes "in the single input line that";
		mes "you're given. So don't press";
		mes "the enter key, just click 'OK.'";
		close;
	case 3:
		mes "[Нилс]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "is the current";
		mes "record holder with";
		mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
		mes "characters. Try to beat";
		mes "that record next time~";
		close;
	case 4:
		mes "[Нилс]";
		mes "Feel free to take on the";
		mes "^ff0000RO Typing Challenge^000000";
		mes "anytime. I'll be here~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Hi, I'm Clarice~";
	mes "How would you like";
	mes "to wager some Apples";
	mes "in a friendly game of Dice?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("Play Dice Game:Learn Dice Game Rules:Cancel")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "I'm up for a game of";
		mes "dice whenever you feel";
		mes "like it. Just talk to me if";
		mes "you ever get hit with the";
		mes "sudden urge to gamble, kay?";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "The rules for the Dice game";
		mes "are pretty simple. First, you";
		mes "place a bet by wagering Apples.";
		mes "You can bet a maximum of 50";
		mes "Apples at a time. To keep things";
		mes "legal, I can only accept Apples.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "But hey, if all that zeny";
		mes "is burning a hole in your";
		mes "pocket, head over to Fruitz";
		mes "and you can buy as many";
		mes "Apples as you want, playah~";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Now, we begin with me";
		mes "rolling two 6-sided dice.";
		mes "When it's your turn, you'll";
		mes "roll two 6-sided dice. After";
		mes "that, both of us will have the";
		mes "option of rolling a third die.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Now here's the important";
		mes "thing. If your total is higher";
		mes "than 12, you'll bust, meaning";
		mes "that you lose. Otherwise, the";
		mes "person with the higher total";
		mes "is the winner. Got it?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Now, you'll be the first";
		mes "to decide whether or not";
		mes "you'll roll the third die. Then,";
		mes "depending on your result, I'll";
		mes "roll my third die... Or maybe not.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "When you win, you'll";
		mes "receive twice as many";
		mes "Apples as you wagered.";
		mes "But if we happen to tie, you";
		mes "get the Apples that you bet";
		mes "returned to you. Fair, right?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "Ooh, so you'll play with";
	mes "me? Great! How many";
	mes "Apples would you like to bet?";
	mes "Remember, you can wager";
	mes "up to 50 Apples. If you'd like";
	mes "to cancel, please enter '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "Changed your mind?";
			mes "I understand. Well then,";
			mes "I hope we can play sometime.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "You can't bet more than";
			mes "50 Apples. Remember, we";
			mes "need to keep these stakes";
			mes "reasonable. Please enter";
			mes "a value no greater than 50.";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "So you'll be";
		mes "betting ^FF0000"+.@amount+"^000000 Apples.";
		mes "Is that right?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "Mm, made a mistake?";
			mes "Alright, please enter the";
			mes "number of Apples you";
			mes "wish to place in this bet";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "I'm sorry, but you";
			mes "don't seem to have";
			mes "enough Apples for this";
			mes "bet... You can't gamble";
			mes "if you can't play, you know.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "Good!";
		mes "Now we can start";
		mes "this game! I'll roll first~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "I got a ^0000FF" + .@table1 + "^000000 and a ^0000FF" + .@table2 + "^000000.";
	mes "That's a total of ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, now it's your turn.";
	next;
	select("Cast Dice.");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, you have ^FF0000" + .@player1 + "^000000 and ^FF0000" + .@player2 + "^000000. The total is ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. We are making an even game. Would you like to cast dice again?";
	} else if (.@playersub > .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, you are currently winning this game. Would you like to cast dice again?";
	} else if(.@tablesub > .@playersub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. I am winning this game. Would you like to cast dice again?";
	}
	next;
	switch (select("Cast dice.:Cancel.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. You lost this game. I am sorry but please try again.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Even though you casted dice again, still your total is smaller than mine. You lost the game. I am sorry and please try again.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. I don't want to take any risk, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Alright.";
			mes "Let me cast the dice again.";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Now it is my turn.";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "I see, you don't want to take risk of losing the game. Okay, let me cast dice again.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "I see, you don't want to take risk of losing this game. Neither do I, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Alright.";
			mes "Let me cast the dice again.";
		}
		else {
			mes "It couldn't hurt to try.";
			mes "Well, I win this time.";
			mes "I'm sorry, let's try play";
			mes "again sometime.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. I lost this game since my total exceeded 12. Let me give you my apples. Congratulations, that was a great game.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you won this game, ^FF0000" + strcharinfo(0) + "^000000. Let me give you my apples. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 this game came out even, ^FF0000" + strcharinfo(0) + "^000000. Let me give you your apple back. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you lost this game, ^FF0000" + strcharinfo(0) + "^000000. I am sorry but please try again.";
		close;
	}
}
