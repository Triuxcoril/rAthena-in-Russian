//===== rAthena Script =======================================
//= Taekwon Job Quest
//===== Description: =========================================
//= [Official Conversion]
//= Taekwon Job Change Quest
//===== Changelogs: ==========================================
//= 1.0 Replaced previous TK Job Quest with this one [Tsuyuki]
//= 1.1 Rewrote to the Aegis Standars. [Samuray22]
//= 1.2 Fixed position the npc is facing. [Kisuka]
//= 1.3 Added Quest Log commands. [Kisuka]
//= 1.4 Removed use of 'goto', and fixed some indentation. [L0ne_W0lf]
//= 1.5 Split into renewal [Lemongrass]
//============================================================ 
//= Translated EN -> RU [Akaineko] 
//============================================================
-	script	Phoenix::PhoenixTKJobChange	-1,{
	if(Upper == 2) {
		mes "[Феникс]";
		mes "Здравствуй, друг.";
		close;
	} else if(Class == Job_Taekwon) {
		mes "[Феникс]";
		mes "Как проходят твои тренировки?";
		mes "Техники оттачиваются и развиваются... Но мы всегда можем опереться на основы.";
		close;
	} else if(Class > Job_Novice) || (Class == Job_Novice && TK_Q == 0) {
		mes "[Феникс]";
		mes "Эта земля. Наш когда-то идеальный Мир теперь завоёван злом: слишком много людей искажены тьмой, слишком много чудовищ терзают невинных...";
		next;
		mes "[Феникс]";
		mes "Хаос, что творится в Мире, это чересчур для обычного человека, нельзя просто взять и в одиночку победить все несчастья.";
		mes "Но бороться -- нужно.";
		next;
		mes "[Феникс]";
		mes "Поэтому я посвятил себя тому, как стать сильнее. Я провёл жизнь в тренировках, чтобы достичь просветления, и разрабатывал искусство владения умом и телом.";
		mes "И теперь я хочу поделиться этим искусством с миром.";
		next;
		mes "[Феникс]";
		mes "Может, я сам и не смогу так сильно изменить Мир, но я никогда не оставлю физических и духовных тренировок. Я знаю: ответ однажды придёт...";
		next;
		if (Class != Job_Novice) {
			mes "[Феникс]";
			mes Sex?"Доблестный путешественник!":"Смелая путешественница!";
			mes "Если у тебя есть знакомый, ещё не выбравший свой жизненный путь, расскажи ему обо мне. Я могу научить его своему искусству...";
			close;
		}
		mes "[Феникс]";
		mes "Если ты ещё не избрал"+(Sex?"":"а")+" свой путь, приглашаю тебя стать моим учеником. Это не будет лёгкий путь, но он ведёт к великой силе...";
		next;
		if(select("Хорошо, я с тобой.","Нет, спасибо.") == 1) {
			if(!callfunc("F_CanChangeJob")) {
				mes "[Феникс]";
				mes "Ты не готов"+(Sex?"":"а")+" начать тренировки. Изучи все основные умения новичка и приходи снова.";
				close;
			}
			mes "[Феникс]";
			mes "Очень хорошо... Я принимаю тебя в ученики. Самый первый этап тренировок -- проверка физического тела, готово ли оно к будущим нагрузкам.";
			next;
			if(BaseLevel > 19) {
				mes "[Феникс]";
				mes "Хм. Вижу, тебе не в новинку физические нагрузки. Очень хорошо. В таком случае перейдём к духовной практике. Сделай глубокий вдох и спокойный выдох. Обратись ко мне, когда будешь готов"+(Sex?"":"а")+".";
				set TK_Q, 2;
				setquest 6001;
				close;
			}
			mes "[Феникс]";
			mes "Ум не привязан к слабостям тела, но нельзя достичь настоящих высот, не обьединив силы ума и тела. Иди и тренируйся дальше, и возвращайся, когда достигнешь ^FF0000следующего базового уровня^000000.";
			next;
			set TAEK_Q, BaseLevel;
			set TK_Q, 1;
			setquest 6000;
			mes "[Феникс]";
			mes "Я понимаю, для новичка это нелёгкое задание, но ты долж"+(Sex?"ен":"на")+" быть готов"+(Sex?"":"а")+" к тяготам нового пути. Жду тебя, "+(Sex?"повзрослевшего":"повзрослевшего")+".";
			close;
		}
		mes "[Феникс]";
		mes "Я тебя понимаю. В жизни есть множество разных путей, но пойти можно только по одному. Я надеюсь, на своём пути ты достигнешь просветления, странни"+(Sex?"к":"ца")+".";
		close;
	}
	switch(TK_Q) {
	case 1:
		if(BaseLevel > TAEK_Q) {
			set TK_Q, 2;
			changequest 6000,6001;
			mes "[Феникс]";
			mes "Молодец. Я чувствую, что теперь ты в резонансе со своей внутренней силой. Это естественный эффект достижения нового уровня. Теперь мы готовы перейти к следующему этапу тренировки.";
			close;
		}
		mes "[Феникс]";
		mes "Тебе нужно заработать ^FF00001ещё один базовый уровень^000000, чтобы доказать, что ты сможешь выдержать трудности этого пути. Никогда не бросай тренировок.";
		close;
	case 2:
		mes "[Феникс]";
		mes "В качестве духовной тренировки я задам тебе несколько вопросов. Расслабься. Давай ответы как можно более честные. Это тест твоей воли и убеждений.";
		next;
		mes "[Феникс]";
		mes "Ученику, практикующему моё искусство, очень важна способность мгновенно принять верное решение. Начнём же.";
		next;
		mes "[Феникс]";
		mes "Когда ты встречаешься с трудностями, как ты на них реагируешь?";
		next;
		switch(select( "Смело встречаю их.:Как-нибудь избегаю.:Останавливаюсь и анализирую.")) {
		case 1:
			mes "[Феникс]";
			mes "Да, это ответ, которого я ждал от тебя. Даже если нельзя решить проблему сразу, такой подход даёт только преимущества. Пусть тебя не останавливают препятствия.";
			next;
			mes "[Феникс]";
			mes "Даже если ты потерпишь поражение, если ты приложил"+(Sex?"":"а")+" все свои силы, ты обретёшь бесценный опыт. Незаинтересованность редко приносит плоды.";
			next;
			mes "[Феникс]";
			mes "Так как ты осознаёшь важность силы воли, мы можем переходить к следующему вопросу.";
			next;
			break;
		case 2:
			mes "[Феникс]";
			mes Sex?"Дурак! Как, по-твоему, ты":"Глупышка! Как, по-твоему, ты";
			mes "сможешь стать сильнее, если бежишь от трудностей? Страх, может, и эволюционно обусловлен, но трусость неприемлема.";
			next;
			mes "[Феникс]";
			mes "Меня разочаровывают твои слова. Не говори мне больше такого. Хм. Поразмысли над тем, что значит храбрость и трусость, и мы продолжим.";
			close;
		case 3:
			mes "[Феникс]";
			mes "Анализировать? Это полезно - но только после поражения. Лучше быстро разбираться с проблемами, как только они появляются. У тебя не всегда будет такая роскошь, как время поразмышлять вволю.";
			next;
			mes "[Феникс]";
			mes "Да, проблемы можно предсказывать и анализировать, но я считаю, что заведомое отступление - это не мудро. Подумай над своими страхами, а ещё над тем, что ты называешь поражением. А потом возвращайся.";
			close;
		}
		mes "[Феникс]";
		mes "В своих странствиях тебе будут встречаться самые разные люди: с разным прошлым, разными взглядами и разными убеждениями. Неизбежно среди них будут и такие, которых ты не сможешь понять.";
		next;
		mes "[Феникс]";
		mes "Также и этот человек не поймёт твоего взгляда на мир. Вот-вот ваши точки зрения столкнутся и возникнет конфликт - как ты себя поведёшь?";
		next;
		switch( select("Настаивать на своей правоте!:Отбросить свою точку зрения:Принять различия и стать мудрее")) {
		case 1:
			mes "[Феникс]";
			mes "Иметь своё мнение очень важно. Но ты долж"+(Sex?"ен":"на")+" понимать, что ты можешь оказаться неправ"+(Sex?"":"а")+", а у противной стороны могут быть и достоинства.";
			next;
			mes "[Феникс]";
			mes "Не бывает единственного верного ответа, а свет истины может отбрасывать множество теней.";
			next;
			mes "[Феникс]";
			mes "Не ограничивай себя одним-единственным взглядом, а свой рост - привязанностью к одной истине. Подумай над этим и возвращайся ко мне.";
			close;
		case 2:
			mes "[Феникс]";
			mes "Уживаться с другими очень важно, но без своих собственных мыслей, взгляда и мнения ничего хорошего не сделать.";
			next;
			mes "[Феникс]";
			mes "Конфликт мировоззрений, протекающий в атмосфере уважения к себе и другим, это возможность расширить свой кругозор и увидеть мир таким, каков он для других.";
			next;
			mes "[Феникс]";
			mes "Поразмысли над этой идеей, о гармонии с собой, о гармонии с окружающими. Возвращайся, когда будешь готов"+(Sex?"":"а")+".";
			close;
		case 3:
			mes "[Феникс]";
			mes "Молодец. Необходимо видеть различия такими, как они есть. А ещё нужно быть честным с самим собой, не забывая ни о критике, ни о милосердии.";
			next;
			mes "[Феникс]";
			mes "Невозможно получить все знания в Мире. Невозможно понять все точки зрения. Но это не означает, что то, что нам непонятно - незначительно.";
			next;
			mes "[Феникс]";
			mes "Тот, с кем мы не согласны, может дать неожиданный ответ. В минуту нашей слабости он может оказаться нам величайшим помощником. Помни это.";
			next;
		}
		mes "[Феникс]";
		mes "Я доволен твоими ответами. Поразмысли ещё немного самостоятельно над всем тем, о чём мы говорили. Когда разум твой обретёт спокойствие, возвращайся ко мне.";
		set TK_Q, 3;
		changequest 6001,6002;
		close;
	case 3:
		mes "[Феникс]";
		mes "Чувствуешь ли ты спокойствие и умиротворение? Пришло время для последнего вопроса. Дай же мне самый честный свой ответ.";
		next;
		mes "[Феникс]";
		mes "^FF0000Готов"+(Sex?"":"а")+" ли ты посвятить себя Искусству, и обладать достоинством его Взгляда?";
		next;
		if( select( "Да.","Нет.") == 1) {
			mes "[Феникс]";
			mes "Очень хорошо. Теперь ты не просто "+(Sex?"ученик":"ученица")+". Тебе доверены силы и ответственность последователя ^FF0000Тхэквон До^000000.";
			next;
			mes "[Феникс]";
			mes "''Тхэквон'' значит ''толкать и пинать'', а ''До'' означает ''искусство''.  Это боевое искусство специализируется на работе руками и ногами.";
			next;
			mes "[Феникс]";
			mes "Используй эту дисциплину во благо телу и уму, учись тому, что тебе лучше подходит. Никогда не бросай тренировок и не позорь Тхеквондо.";
			next;
			completequest 6002;
			callfunc "Job_Change",Job_Taekwon;
			callfunc "F_ClearJobVar";
			getitem 2101,1; // Guard[0]
			mes "[Феникс]";
			mes "Ты ещё юн"+(Sex?"":"а")+" и, полагаю, захочешь название для своей профессии. Мм, в таком случае, ты";
			if(Sex == 0) {
				mes "теперь ^FF0000Тхэквондистка^000000.";
			} else {
				mes "теперь ^FF0000Тхэквондист^000000.";
			}
			mes "Да, звучит хорошо.";
			next;
			mes "[Феникс]";
			mes "Возьми эту тренировочную форму и систему защиты: бережно относись к этим дарам. В своих путешествиях просвещай остальных о нашем искусстве и учись у них в ответ.";
			next;
			mes "[Феникс]";
			mes "Пришло время тебе отправиться в свой собственный путь, на поиски испытаний и своей силы. Будь достойн"+(Sex?"ым":"ой")+" последовател"+(Sex?"ем":"ьницей")+" Тхеквондо...";
			next;
			mes "[Феникс]";
			mes "Очень хорошо. ";
			mes "Желаю тебе удачи. ";
			mes "Надеюсь, нам доведётся свидеться вновь, "+strcharinfo(0)+".";
			close;
		}
		mes "[Феникс]";
		mes "Хм, возможно, ты не совсем готов"+(Sex?"":"а")+" перейти из учеников в практиканты. Когда почувствуешь, что время пришло, обратись ко мне снова.";
		close;
	}
}

